/////////////////////////////////////////////////////////////////////////////////////////////////////
///     Spells used by fiends; broken off because many are used by Smarter Mages and by Talents of Faerun
///
/////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION fiend_spells_basic BEGIN

   LAF check_label STR_VAR label="dw#fiend_spells_made" RET value END
   ACTION_IF !value BEGIN

       LAF make_label STR_VAR label="dw#fiend_spells_made" END

	// install possibly-missing spells
	ACTION_IF !FILE_EXISTS_IN_GAME "%SUCCUBUS_TELEPORT%.spl" BEGIN
		COPY "%MOD_FOLDER%/%component_loc%/resource/%SUCCUBUS_TELEPORT%.spl" override
	END
	ACTION_IF !FILE_EXISTS_IN_GAME "%DEATHKNIGHT_FIREBALL%.spl" BEGIN
		COPY "%MOD_FOLDER%/%component_loc%/resource/%DEATHKNIGHT_FIREBALL%.spl" override
	END

       LAF spl_make_innate_repeating_spell INT_VAR overwrite=1 allow_missing=1 STR_VAR arguments="%TANARI_PARALYZE% %TANARI_VAMPIRIC_TOUCH% %TANARI_DEATH_GAZE%" END
       

	   ACTION_CLEAR_ARRAY patch_data
	   ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
          DEATHKNIGHT_FIREBALL=>INNATE_FIEND_FIREBALL
          WIZARD_IMPROVED_INVISIBILITY=>INNATE_FIEND_IMPROVED_INVISIBILITY
          WIZARD_TRUE_SIGHT=>INNATE_FIEND_TRUE_SIGHT
          WIZARD_DOMINATION=>INNATE_FIEND_DOMINATION
		  WIZARD_RAY_OF_ENFEEBLEMENT=>INNATE_FIEND_RAY_OF_ENFEEBLEMENT
       END
       ACTION_PHP_EACH patch_data AS key=>value BEGIN
			OUTER_SPRINT old_spell EVAL "%%key%%"
            LAF spl_make_innate_repeating_spell INT_VAR allow_missing=1 STR_VAR arguments="%old_spell%=>%value%" END
       END

	   COPY_EXISTING "%TANARI_DEATH_GAZE%.spl" override
			WRITE_ASCII 0x3a spwi605c
			LPF ALTER_SPELL_HEADER STR_VAR icon=spwi605b END
	   IF_EXISTS

       COPY_EXISTING "%SUCCUBUS_TELEPORT%.spl" "override/dw#fitel.spl" // fiendish teleport
		LPF ALTER_EFFECT INT_VAR match_opcode=124 timing=1 duration=0 END
 
   END
END
