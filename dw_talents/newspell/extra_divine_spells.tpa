///////////////////////////////////////////////////////
// This is an edited version of the new_spells macro in Divine Remix
//
// Because we're using the DR base code, we use a lower-level code style
// than elsewhere in the mod


DEFINE_ACTION_FUNCTION extra_divine_spells BEGIN
   
	LAF check_label STR_VAR label=dw_tof_divine_spells RET value END
	ACTION_IF !value BEGIN
	
		LAF make_label STR_VAR label=dw_tof_divine_spells END
	// make sure this resource is present

		ACTION_IF NOT FILE_EXISTS_IN_GAME ~eff_p32.wav~ THEN BEGIN

		  COPY ~%MOD_FOLDER%/%component_loc%/resource/eff_p32.wav~ ~override~

		END



		OUTER_SPRINT level_match @2027 

		//////////////////////////////
		// faerie fire
		//////////////////////////////

			LAF run STR_VAR file=faerie_fire locbase=shared_som END
			COPY_EXISTING "hidespl.2da" override
			CLEAR_IDS_MAP
			
			LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name=CLERIC_FAERIE_FIRE RET spell_res END
			COPY_EXISTING "hidespl.2da" override
				REPLACE_TEXTUALLY "%spell_res%.*[%WNL%%LNL%%MNL%]" ""
			BUT_ONLY
			
		//////////////////////////////
		// protection from good
		//////////////////////////////

		// first handle the IWD version of the spell, sitting in an NPC slot

		COPY_EXISTING "spell.ids" override
			REPLACE_TEXTUALLY "1199[ %TAB%]+CLERIC_PROTECT_FROM_GOOD" "1199 CLERIC_PROTECT_FROM_GOOD_NPC" 
		BUT_ONLY
		CLEAR_IDS_MAP

		ACTION_IF IDS_OF_SYMBOL (~spell~ ~CLERIC_PROTECT_FROM_GOOD~) < 0 BEGIN // only if not present 

			COPY_EXISTING "%CLERIC_PROTECT_FROM_EVIL%.spl" "%workspace%/protect_from_good.spl"
				SAY NAME1 @196
				SAY NAME2 @196
				SAY UNIDENTIFIED_DESC @197
				SAY DESC @197
				WRITE_ASCII 0x3a ~cdprgooc~
				LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@201) END
				LPF ALTER_EFFECT INT_VAR match_opcode=219 parameter1=1 END
				LPF DELETE_EFFECT INT_VAR match_opcode=100 END
				LPF ALTER_SPELL_HEADER STR_VAR icon=cdprgoob END
		 
		   LAF extended_add_spell STR_VAR id=CLERIC_PROTECT_FROM_GOOD file=protect_from_good path="%workspace%" RET resref END
		   LAF spl_make_scroll STR_VAR spell_resref="%resref%" END
			// copy over icons
			
			LAF install STR_VAR files="cdprgoob.bam cdprgooc.bam" location=dr END

		END

		//////////////////////////////
		// protection from good 10' radius
		//////////////////////////////

		ACTION_IF IDS_OF_SYMBOL (~spell~ ~CLERIC_PROTECTION_FROM_GOOD_10_FOOT~) < 0 BEGIN // only if not present 

			COPY_EXISTING "%CLERIC_PROTECTION_FROM_EVIL_10_FOOT%.spl" "%workspace%/protect_from_good_10_foot.spl"
				SAY NAME1 @198
				SAY NAME2 @198
				SAY UNIDENTIFIED_DESC @199
				SAY DESC @199
				WRITE_ASCII 0x3a ~cdprgd1c~
				LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@201) END
				LPF ALTER_EFFECT INT_VAR match_opcode=219 parameter1=1 END
				LPF DELETE_EFFECT INT_VAR match_opcode=100 END
				LPF ALTER_SPELL_HEADER STR_VAR icon=cdprgd1b END

		   LAF extended_add_spell STR_VAR id=CLERIC_PROTECTION_FROM_GOOD_10_FOOT file=protect_from_good_10_foot path="%workspace%" RET resref END
		   LAF spl_make_scroll STR_VAR spell_resref="%resref%" END
			// copy over icons
			
			LAF install STR_VAR files="cdprgd1b.bam cdprgd1c.bam" location=dr END



		END

		//////////////////////////////
		// Strength of Stone
		//////////////////////////////


		ACTION_IF IDS_OF_SYMBOL (~spell~ ~CLERIC_STRENGTH_OF_STONE~) < 0 BEGIN // only if not present or overwrite selected


		   
		  COPY "%MOD_FOLDER%/%component_loc%/dr/nmstrstn.spl" "%workspace%"
			SAY NAME1 @175
			SAY NAME2 @175
			SAY UNIDENTIFIED_DESC @176
			SAY DESC @176
			// restrict target to self
			LPF ALTER_ITEM_HEADER INT_VAR target=5 END
			// change strength
			LPF DELETE_EFFECT INT_VAR match_opcode=44 match_parameter1=17 END
			LPF DELETE_EFFECT INT_VAR match_opcode=44 match_parameter1=18 END
			LPF ALTER_EFFECT INT_VAR match_opcode=44 parameter1=4 parameter2=0 END
			// impose movement penalty
			LPF CLONE_EFFECT INT_VAR match_opcode=44 opcode=176 parameter1=50 parameter2=enhanced_edition?5:2 END


		   LAF extended_add_spell STR_VAR id=CLERIC_STRENGTH_OF_STONE file=nmstrstn path="%workspace%" RET CLERIC_STRENGTH_OF_STONE=resref END
		   LAF spl_make_scroll STR_VAR spell_resref="%CLERIC_STRENGTH_OF_STONE%" END
			
			// copy over icons
			
			LAF install STR_VAR files="nmstrstb.bam nmstrstc.bam" location=dr END

		END

		//////////////////////////////
		// Resist Acid and Corrosion
		//////////////////////////////
			
		ACTION_IF IDS_OF_SYMBOL (~spell~ ~CLERIC_RESIST_ACID_AND_CORROSION~) < 0 BEGIN
			

			COPY "%MOD_FOLDER%/%component_loc%/dr/nmresacd.spl" "%workspace%"
				SAY NAME1 @173
				SAY NAME2 @173
				SAY UNIDENTIFIED_DESC @174
				SAY DESC @174

			LAF extended_add_spell STR_VAR id=CLERIC_RESIST_ACID_AND_CORROSION file=nmresacd path="%workspace%" RET CLERIC_RESIST_ACID_AND_CORROSION=resref END
			LAF spl_make_scroll STR_VAR spell_resref="%CLERIC_RESIST_ACID_AND_CORROSION%" END


			LAF install STR_VAR files="nmresacb.bam nmresacc.bam" location=dr END
			
			
		END
			
		//////////////////////////////
		// Unholy Shield
		//////////////////////////////

		ACTION_IF IDS_OF_SYMBOL (~spell~ ~CLERIC_FIENDISH_WARDING~) < 0 BEGIN
			
			OUTER_SPRINT not_evil @212
			OUTER_SPRINT only_evil @211
			COPY_EXISTING "%CLERIC_SHIELD_OF_THE_ARCHONS%.spl" "%workspace%/unholy_ward.spl"
				READ_STRREF NAME1 oldname
				SPRINT newname @209
				SAY NAME1 @209
				READ_STRREF UNIDENTIFIED_DESC desc
				INNER_PATCH_SAVE desc "%desc%" BEGIN
					REPLACE_TEXTUALLY "%oldname%" "%newname%"
				END
				SAY UNIDENTIFIED_DESC "%desc%%WNL%%only_evil%"
				LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@210) END
				LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=50 parameter1=0x00460000 END
				WRITE_BYTE 0x1e 0b00001100 // only evil
				WRITE_BYTE 0x21 0b10000000 // not druids
			LAF extended_add_spell STR_VAR id=CLERIC_FIENDISH_WARDING file=unholy_ward path="%workspace%" RET resref END
			OUTER_SPRINT CLERIC_FIENDISH_WARDING "%resref%"
			LAF spl_make_scroll STR_VAR spell_resref="%resref%" END
			
			COPY_EXISTING "%CLERIC_SHIELD_OF_THE_ARCHONS%.spl"  override
				WRITE_BYTE 0x1e 0b00000010 // not evil
				WRITE_BYTE 0x21 0b10000000 // not druids
				READ_STRREF UNIDENTIFIED_DESC desc
				SAY UNIDENTIFIED_DESC "%desc%%WNL%%not_evil%"
			BUT_ONLY
			
			ACTION_IF enhanced_edition BEGIN
				COPY_EXISTING "%CLERIC_SHIELD_OF_THE_ARCHONS%B.spl" "override/%CLERIC_FIENDISH_WARDING%B.spl"
					LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource="%CLERIC_FIENDISH_WARDING%" END
			END
				
		END

	END


END