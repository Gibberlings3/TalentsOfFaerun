DEFINE_ACTION_FUNCTION kit_rebalance BEGIN

	
	ACTION_FOR_EACH kit IN 
		dragon_disciple
		totemic_druid
		berserker 
		barbarian
		shapeshifter
		wizard_slayer
		undead_hunter
		cavalier
		inquisitor 
		blade
		jester
		skald
		assassin
	BEGIN
		LAF check_ini STR_VAR ini="rebalance_%kit%" RET value END
		ACTION_IF value BEGIN
			LAF "rebalance_%kit%" END	
			LAF make_label STR_VAR label="dw#rebalance_%kit%" END
		END
		
	END



	// re-externalize proficiencies if appropriate
	ACTION_IF FILE_EXISTS "%data_loc_shared%/weapprof.2da" BEGIN
		LAF externalize_weapprof END
	END




END

///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////
///  SORCERER KITS
///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////
///  Dragon Disciple - hold immunity at L7, charm immunity at L13, more breaths
///////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION rebalance_dragon_disciple BEGIN

	// make immunity to charm, hold
	
	// charm
	spl.make[%spl_dragon_disciple_charm_immunity%]
	[
		k_name:=@7
		m.ab.add{}
		m.immunity_effect{charm}
	]
	
	// hold
	
	spl.make[%spl_dragon_disciple_hold_immunity%]
	[
		k_name:=@8
		m.ab.add{}
		m.immunity_effect{hold}
	]

	//apply them, and append description accordingly

	kit.edit[DRAGON_DISCIPLE]
	[
		k.kit_apply_powers{%spl_dragon_disciple_hold_immunity% 7,%spl_dragon_disciple_charm_immunity% 13}
		k.kit_grant_powers{SPDD03 6 6 18}
	]
	
	
	array.new[dd_extras]
	[
		BLACK=>A
		GREEN=>P
		BLUE=>L
		WHITE=>C
	]
	
	LAF patch_dragon_disciple_description STR_VAR kit=DRAGON_DISCIPLE END
	ACTION_PHP_EACH dd_extras AS color=>letter BEGIN
		OUTER_SPRINT kit "DW_DRAGON_DISCIPLE_%color%"
		ACTION_IF RESOURCE_CONTAINS "kitlist.2da" "%kit%" BEGIN
			OUTER_SPRINT breath_weapon "DWDD%letter%B1"
			kit.edit[%kit%]
			[
				k.kit_apply_powers{%spl_dragon_disciple_hold_immunity% 7,%spl_dragon_disciple_charm_immunity% 13}
				k.kit_grant_powers{%breath_weapon% 6 6 18}
			]							
			LAF patch_dragon_disciple_description STR_VAR kit END
		END
	END


END

DEFINE_ACTION_FUNCTION patch_dragon_disciple_description STR_VAR kit="" BEGIN
  LAF 2daq_extract INT_VAR rowname_column=1 STR_VAR resref=kitlist row="%kit%" column=HELP RET strref=value END

  ACTION_GET_STRREF strref desc
  OUTER_PATCH_SAVE desc "%desc%" BEGIN
	SPRINT level_8_string @38
	SPRINT level_15_string @39
	SPRINT level_7_string @40
	SPRINT level_13_string @41
	first_swap=0
	second_swap=0
	REPLACE_EVALUATE "^\(.*\)\(%level_8_string%.*\)" BEGIN
		PATCH_IF first_swap BEGIN
			SPRINT output "%MATCH1%%MATCH2%"
		END ELSE BEGIN
			first_swap=1
			SPRINT output "%MATCH1%%level_7_string%%WNL%%MATCH1%%MATCH2%"
		END
	END
	"%output%"
	REPLACE_EVALUATE "^\(.*\)\(%level_15_string%.*\)" BEGIN
		PATCH_IF second_swap BEGIN
			SPRINT output "%MATCH1%%MATCH2%"
		END ELSE BEGIN
			second_swap=1
			SPRINT output "%MATCH1%%level_13_string%%WNL%%MATCH1%%MATCH2%"
		END
	END
	"%output%"
	
	array.new{dd_swaps}
	{
		59=>62
		60=>63
		61=>64
	}
	PHP_EACH dd_swaps AS old=>add BEGIN
		old:=(AT old)
		add:=(AT add)
		REPLACE_TEXTUALLY "%old%" "%old% %add%"
	END
	
  END
  STRING_SET_EVALUATE strref "%desc%"
END

///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////
///  FIGHTER KITS
///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////////////
/// Wizard Slayer - magic resistance changed to base + fixed per-level, values are 20% and 1% by
/// default but are externalised
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_wizard_slayer BEGIN

// get values 

	LAF check_ini STR_VAR ini=wizard_slayer_base_resistance RET resist_base=value END
	LAF check_ini STR_VAR ini=wizard_slayer_bonus_resistance RET resist_bonus=value END

// make spells

	spl.copy[SPCL131=>%spl_wizard_slayer_base_resistance%]
	[
		m.ab_fx.alter{s_parameter2=1 s_parameter1=resist_base}
	]
	
	
	spl.copy[SPCL131=>%spl_wizard_slayer_incremental_resistance%]
	[
		m.ab_fx.alter{s_parameter1=resist_bonus}
	]

// apply to kit

	kit.edit[WIZARD_SLAYER|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{SPCL131 SPCL819}
		k.kit_apply_powers{%spl_wizard_slayer_base_resistance% 1,%spl_wizard_slayer_incremental_resistance% 2 1}
		k_description:=@12
		k_description=k_description.patch{
			REPLACE_TEXTUALLY BASE_PLACEHOLDER "%resist_base%"
			REPLACE_TEXTUALLY INCREMENT_PLACEHOLDER "%resist_bonus%"		
		}
	]


END

///////////////////////////////////////////////////////////////////////////////////////
/// Berserker - Rage gives + (5+level) hp, not a flat +15; powers kick in at various levels
///////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION rebalance_berserker BEGIN

	// define the levels at which effects kick in
	
	ACTION_DEFINE_ASSOCIATIVE_ARRAY rage_level BEGIN
		fear=>1
		sleep=>2
		hold=>4
		charm=>6
		feeblemind=>6
		confusion=>8
		stun=>10
		leveldrain=>12
		imprisoned=>14
		mazed=>14
	END
	ACTION_DEFINE_ASSOCIATIVE_ARRAY rage_opcode BEGIN
		fear=>24
		sleep=>39
		hold=>175
		charm=>5
		feeblemind=>76
		confusion=>3
		stun=>45
		leveldrain=>216
		imprisoned=>211
		mazed=>213
	END

	// edit the Berserker Rage spell
	
	
	spl.edit[%BERSERKER_RAGE%|edit_strrefs_in_place:i=1]
	[
		m_description:=@2
		m_description=m_description.patch
		{
			rage_desc:=@5
			REPLACE_TEXTUALLY "RAGE_PLACEHOLDER" "%rage_desc%"	
		}
		m.ab.clone{s_level=entry_index+2|number:i=24}
		// clear out immunity effects (also does hp)
		m.ab_fx.alter{rage_effect_cull}
		m.ab_fx.delete{s_opcode=999}
		// add immunities by level
		PHP_EACH rage_level AS id=>lvl BEGIN
			opc=$rage_opcode("%id%")
			m.ab_fx.add{s_opcode=101 s_parameter2=opc s_duration=60 s_target=1|match_parent="p_level>=lvl"}
		END
		// secondary immunity effects
		m.immunity_effect{fear sleep hold charm feeblemind confusion stun energy_drain imprisoned mazed|permanent:i=0}
	]

	
	// change the string in the secondary spell 
	
	ACTION_IF GAME_IS IWDEE BEGIN
		OUTER_SET strref=36023
	END ELSE 
	ACTION_IF is_bg2 BEGIN
		OUTER_SET strref=20726
	END ELSE BEGIN
		OUTER_SET strref=25830
	END
	STRING_SET_EVALUATE strref @1
	
	// edit the kit description
		
	LAF 2daq_extract STR_VAR resref="clastext" row=BERSERKER column=DESCSTR RET strref2=value END

	OUTER_SPRINT desc @3
	OUTER_PATCH_SAVE desc "%desc%" BEGIN
		rage_desc:=@5
		REPLACE_TEXTUALLY "RAGE_PLACEHOLDER" "%rage_desc%"
	END
	STRING_SET_EVALUATE strref2 "%desc%"
END

///////////////////////////////////////////////////////////////////////////////////////
/// Barbarian - Rage powers kick in at various levels; +5% to DR at L7
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_barbarian BEGIN

	// define the levels at which effects kick in
	
	ACTION_DEFINE_ASSOCIATIVE_ARRAY rage_level BEGIN
		fear=>1
		sleep=>2
		hold=>4
		charm=>6
		feeblemind=>6
		confusion=>8
		stun=>10
		leveldrain=>12
		imprisoned=>14
		mazed=>14
	END
	ACTION_DEFINE_ASSOCIATIVE_ARRAY rage_opcode BEGIN
		fear=>24
		sleep=>39
		hold=>175
		charm=>5
		feeblemind=>76
		confusion=>3
		stun=>45
		leveldrain=>216
		imprisoned=>211
		mazed=>213
	END

	// edit the Barbarian Rage spell
	
	spl.edit[%BARBARIAN_RAGE%|edit_strrefs_in_place:i=1]
	[
		m_description:=@11
		m_description=m_description.patch
		{
			rage_desc:=@9
			REPLACE_TEXTUALLY "RAGE_PLACEHOLDER" "%rage_desc%"	
		}
		m.ab.clone{s_level=entry_index+2|number:i=13}
		// clear out immunity effects
		m.ab_fx.alter{rage_effect_cull}
		m.ab_fx.delete{s_opcode=999}
		// add immunities by level
		PHP_EACH rage_level AS id=>lvl BEGIN
			opc=$rage_opcode("%id%")
			m.ab_fx.add{s_opcode=101 s_parameter2=opc s_duration=60 s_target=1|match_parent="p_level>=lvl"}
		END
		// secondary immunity effects
		m.immunity_effect{fear sleep hold charm feeblemind confusion stun energy_drain imprisoned mazed|permanent:i=0}
	]
	
	// make DR start earlier; edit the kit description
	
	kit.edit[BARBARIAN|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{spcl153}
		k.kit_apply_powers{spcl154 7,spcl154 11}
		rage_desc:=@9
		k_description:=@10
		k_description=k_description.patch{
			REPLACE_TEXTUALLY "RAGE_PLACEHOLDER" "%rage_desc%"		
		}		
	]

END

///////////////////////////////////////////////////////////////////////////////////////
/// Rage-nerfing function, shared by barbarian and berserker
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION rage_effect_cull
BEGIN
	PATCH_MATCH "%s_opcode%" WITH
	106 161 23 321 267 169 101 142 240 296 46 139 BEGIN
		WRITE_SHORT 0x0 999
	END
	12 17 18 BEGIN
		WRITE_LONG 0x4 (5+p_level)
	END
	DEFAULT
	END
END



DEFINE_PATCH_FUNCTION rage_effect_cull_old
BEGIN
	SET wipe=0
	PATCH_IF (s_opcode=106 || s_opcode=161 || s_opcode=23 || s_opcode=321 ) && p_level<rage_level_fear BEGIN
		wipe=1
	END ELSE
	PATCH_IF s_opcode=267 THEN BEGIN
		wipe=((s_parameter1=1280 ||s_parameter1=14043 || s_parameter1=25862 || s_parameter1=26050) && p_level<rage_level_stun)
				|| ((s_parameter1=35544 || s_parameter1=35545 || s_parameter1=14672 || s_parameter1=14780 || s_parameter1=26206 || s_parameter1=17392 || s_parameter1=37801 ) && p_level<rage_level_charm)
				|| ((s_parameter1=35484 || s_parameter1=14007 || s_parameter1=17427 || s_parameter1=20568 || s_parameter1=25818) && p_level<rage_level_fear)
				|| ((s_parameter1=37603 || s_parameter1=37604 || s_parameter1=26184 || s_parameter1=25807) && p_level<rage_level_confusion)
				|| ((s_parameter1=37613 || s_parameter1=37340 || s_parameter1=26371 ) && p_level<rage_level_sleep)
				|| ((s_parameter1=14102 || s_parameter1=14650 || s_parameter1=35542 || s_parameter1=35606 ||s_parameter1=25866 ||s_parameter1=31799 || s_parameter1=25806 || s_parameter1=25805 || s_parameter1=25804 || s_parameter1=25803 || s_parameter1=25802) && p_level<rage_level_hold)
				|| ((s_parameter1=35498 || s_parameter1=35497 || s_parameter1=35496 || s_parameter1=35472 || s_parameter1=35495) && p_level<rage_level_leveldrain)
	END ELSE
	PATCH_IF s_opcode=169 THEN BEGIN
		wipe=((s_parameter2=1 || s_parameter2=0 || s_parameter2=43) && p_level<rage_level_charm) 
				|| ((s_parameter2=55 || s_parameter2=44) && p_level<rage_level_stun)
				|| ((s_parameter2=130 || s_parameter2=14) && p_level<rage_level_sleep)
				|| (s_parameter2=36 && p_level<rage_level_fear)
				|| ((s_parameter2=3 || s_parameter2=2 || s_parameter2=47) && p_level<rage_level_confusion)
				|| ((s_parameter2=79 || s_parameter2=78) && p_level<rage_level_imprisonmaze)
				|| (s_parameter2=48 || p_level<rage_level_feeblemind)
				|| (s_parameter2=13 && p_level<rage_level_hold)
				|| ((s_parameter2=59 || s_parameter2=53) && p_level<rage_level_leveldrain)

	END ELSE
	PATCH_IF s_opcode=101 THEN BEGIN
		wipe=((s_parameter2=45 || s_parameter2=210) && p_level<rage_level_stun ) 
				|| ((s_parameter2=217 || s_parameter2=39 || s_parameter2=130) && p_level<rage_level_sleep) 
				|| (s_parameter2=5 && p_level<rage_level_charm)
				|| ((s_parameter2=24 || s_parameter2=106 || s_parameter2=23) && p_level<rage_level_fear)
				|| ((s_parameter2=211 || s_parameter2=213) && p_level<rage_level_imprisonmaze)
				|| ((s_parameter2=109 || s_parameter2=175) && p_level<rage_level_hold)
				|| (s_parameter2=128 && p_level<rage_level_confusion)
				|| (s_parameter2=76 && p_level<rage_level_feeblemind)
				|| (s_parameter2=216 && p_level<rage_level_leveldrain)
	END ELSE
	PATCH_IF s_opcode=142 THEN BEGIN
		wipe=(s_parameter2=52 && p_level<rage_level_charm)
				|| (s_parameter2=37 && p_level<rage_level_fear)
				|| (s_parameter2=90 && p_level<rage_level_leveldrain)
	END ELSE
	PATCH_IF s_opcode=240 THEN BEGIN
		wipe=(s_parameter2=36 && p_level<rage_level_fear)
				|| (s_parameter2=55 && p_level<rage_level_stun)
	END ELSE
	PATCH_IF s_opcode=296 THEN BEGIN
		PATCH_MATCH "%s_resource%" WITH
		SPNWCHRM BEGIN 
			wipe=(p_level<rage_level_charm)
		END
		CDHORROR BEGIN
			wipe=(p_level<rage_level_fear)
		END
		SPFLAYER BEGIN
			wipe=(p_level<rage_level_stun)
		END
		SPCONFUS CONFUSH BEGIN
			wipe=(p_level<rage_level_confusion)
		END
		SPMINDAT PARALHAT BEGIN
			wipe=(p_level<rage_level_hold)
		END
		DEFAULT
		END
	END ELSE 
	PATCH_IF s_opcode=46 BEGIN
		wipe=(p_level<rage_level_stun)
	END ELSE
	PATCH_IF (s_opcode=12 || s_opcode=17 || s_opcode=18) BEGIN
		s_parameter1=5+p_level
	END
	PATCH_IF wipe BEGIN 
		WRITE_LONG 0x0 999
	END
END

///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////
/// PALADIN KITS
///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////
// Cavalier - spread out immunities, make damage resistance, less BG2-specific, increase
// range of creatures you get +3 to, evasion vs breath weapon
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_cavalier BEGIN

	// evade dragon breath
	
	LAF ds_resolve_stat STR_VAR id=DW_CAVALIER_EVASION RET splstate=stat_ind END

	spl.make[%spl_cavalier_evasion%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=328 s_target=2 s_timing=9 s_special=1 s_parameter2=splstate}
	]
	
	ACTION_FOR_EACH spell IN 
	SHADOW_DRAGON_BREATH SILVER_DRAGON_BREATH_COLD RED_DRAGON_HIT BLACK_DRAGON_BREATH BLUE_DRAGON_BREATH BROWN_DRAGON_BREATH GREEN_DRAGON_BREATH drgrbrht INNATE_DRAGON_BREATH bdwyrmb
	BEGIN
		ACTION_IF VARIABLE_IS_SET "%spell%" BEGIN
			OUTER_SPRINT spell EVAL "%%spell%%"
		END
		ACTION_IF FILE_EXISTS_IN_GAME "%spell%" BEGIN
			COPY_EXISTING "%spell%.spl" override
				LPF enable_evasion INT_VAR splstate END
			BUT_ONLY
		END
	END
	
	// separate out immunity to fear, poison and charm, and damage resistance
	
	spl.make[%spl_cavalier_fear_immunity%]
	[
		m.ab.add{}
		m.immunity_effect{fear}
	]

	spl.make[%spl_cavalier_poison_immunity%]
	[
		m.ab.add{}
		k_name:=@13
		m.immunity_effect{poison}
	]

	spl.make[%spl_cavalier_charm_immunity%]
	[
		m.ab.add{}
		k_name:=@7
		m.immunity_effect{charm}
	]

	spl.make[%spl_cavalier_resistance_increment%]
	[
		m.ab.add{}
		k_name:=@14
		m.ab_fx.add{s_opcode=27+entry_index s_target=2 s_timing=9 s_parameter1=5|number:i=4}
	]

	spl.make[%spl_cavalier_resistance_base%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=27+entry_index s_target=2 s_timing=9 s_parameter1=10 s_parameter2=1|number:i=4}
		array.new{param_array}{0=>24 1=>25 2=>27 3=>16}
		m.ab_fx.add{s_opcode=142 s_target=2 s_timing=9 s_parameter2=$param_array("%entry_index%")|number:i=4}
	]
	spl.edit[spcl221]
	[
		m.ab_fx.delete{!(s_opcode=177)}
	]
	
	

	// at-will Remove Fear
	
	spl.edit[%CAVALIER_REMOVE_FEAR%]
	[
		m.ab_fx.add{s_opcode=172 s_target=1 s_timing=1 s_resource:="%sfo_filename%"}
		m.ab_fx.add{s_opcode=171 s_target=1 s_timing=1 s_resource:="%sfo_filename%"}
	]

	
	// more nuanced attack forms
	
	ACTION_FOR_EACH race IN darkplanatar antisolar salamander BEGIN
		OUTER_SET race_id = IDS_OF_SYMBOL (race "%race%")
		OUTER_SPRINT eff_hit EVAL "%eff_cavalier_%race%_hit%" 
		OUTER_SPRINT eff_dmg EVAL "%eff_cavalier_%race%_damage%" 
		
		eff.copy[draghit=>%eff_hit% dragdam3=>%eff_dmg%][m_parameter1=race_id]		

		spl.edit[spcl221]
		[
			m.ab_fx.clone{s_resource:="%eff_hit%"|match="~%s_resource%~ STR_EQ draghit"}
			m.ab_fx.clone{s_resource:="%eff_dmg%"|match="~%s_resource%~ STR_EQ dragdam3"}
		]
	END
	ACTION_FOR_EACH class IN red_dragon shadow_dragon genie_dao genie_efreeti genie_noble_efreet BEGIN
		OUTER_SET class_id = IDS_OF_SYMBOL (class "%class%")
		OUTER_SPRINT eff_hit EVAL "%eff_cavalier_%class%_hit%" 
		OUTER_SPRINT eff_dmg EVAL "%eff_cavalier_%class%_damage%" 

		eff.copy[draghit=>%eff_hit% dragdam3=>%eff_dmg%][m_parameter1=class_id m_parameter2=5]		

		spl.edit[spcl221]
		[
			m.ab_fx.clone{s_resource:="%eff_hit%"|match="~%s_resource%~ STR_EQ draghit"}
			m.ab_fx.clone{s_resource:="%eff_dmg%"|match="~%s_resource%~ STR_EQ dragdam3"}
		]
		
	END
	
	spl.edit[spcl221]
	[
		m.ab_fx.delete{"%s_resource%" STR_EQ draghit OR "%s_resource%" STR_EQ dragdam3}
	]

	// implement kit changes
	
	kit.edit[CAVALIER|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{%CAVALIER_REMOVE_FEAR%}
		k.kit_grant_powers{%CAVALIER_REMOVE_FEAR% 3}
		k.kit_apply_powers{%spl_cavalier_fear_immunity% 1,%spl_cavalier_resistance_base% 1,%spl_cavalier_resistance_increment% 6 5 25,%spl_cavalier_poison_immunity% 5,%spl_cavalier_charm_immunity% 7,%spl_cavalier_evasion% 12}
		k_desc:=@15
		IF {FILE_EXISTS_IN_GAME "%spl_smite_evil_cavalier%.spl"}
			// restore description of Smite Evil
		{{
			PATCH_WITH_TRA "%scs_tra_loc%/english/class_race.tra" "%scs_tra_loc%/%LANGUAGE%/class_race.tra" BEGIN
				new:=@716
				adv:=@714
			END
			k_desc=k_desc.patch{REPLACE_TEXTUALLY "^%adv%" "%adv%%WNL%%new%"}
		}}
	]

	

END

///////////////////////////////////////////////////////////////////////////////////////
/// Inquisitor - Truesight delayed to L9, DM to L3. Detect Invisibility at L1,5. Charm immunity at level 7, hold at level 5.
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_inquisitor BEGIN

	// make the new Detect Invisibility effect
	LAF splconv STR_VAR spell="%WIZARD_DETECT_INVISIBILITY%" new_type=innate new_resref="%spl_inquisitor_detect_invisibility%" END
	
	// make new effects that grant charm, hold immunity 
	
	// charm
	spl.make[%spl_inquisitor_charm_immunity%]
	[
		k_name:=@7
		m.ab.add{}
		m.immunity_effect{charm}
	]
	
	// hold
	spl.make[%spl_inquisitor_hold_immunity%]
	[
		k_name:=@8
		m.ab.add{}
		m.immunity_effect{hold}
	]

	
	// sort out the kit
	
	kit.edit[INQUISITOR|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{%INQUIS_TRUE_SIGHT% %INQUIS_DISPEL_MAGIC% spcl233b}
		k.kit_grant_powers{%spl_inquisitor_detect_invisibility% 1,%spl_inquisitor_detect_invisibility% 5,%INQUIS_DISPEL_MAGIC% 3 4,%INQUIS_TRUE_SIGHT% 9 4}
		k.kit_apply_powers{%spl_inquisitor_hold_immunity% 5,%spl_inquisitor_charm_immunity% 7}
		k_desc:=@6
	]

END

///////////////////////////////////////////////////////////////////////////////////////
/// Undead Hunter - bonus to hit starts at +1 and goes up, hold immunity at L3, level drain immunity at L7
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_undead_hunter BEGIN

	// make the new immunity powers
	
	// hold

	spl.make[%spl_undead_hunter_hold_immunity%]
	[
		k_name:=@8
		m.ab.add{}
		m.immunity_effect{hold}
	]
	
	// energy drain
	spl.make[%spl_undead_hunter_energy_drain_immunity%]
	[
		k_name:=@17
		m.ab.add{}
		m.immunity_effect{energy_drain}
	]
	
	// make +1 and +2 versions of the undhit3, unddam3 effects
	
	eff.copy[undhit3=>%eff_undead_hunter_hit_plus_one% unddam3=>%eff_undead_hunter_damage_plus_one%][s_parameter3=1]

	// make applicators for them
	
	COPY_EXISTING "spcl241.spl" "override/%spl_undead_hunter_plus_one%.spl" 
		REPLACE_TEXTUALLY undhit3 "%eff_undead_hunter_hit_plus_one%" (8)
		REPLACE_TEXTUALLY unddam3 "%eff_undead_hunter_damage_plus_one%" (8)
	BUT_ONLY
	
	// sort out the kit itself
	
	kit.edit[UNDEAD_HUNTER|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{spcl241 spcl242}
		k.kit_apply_powers{%spl_undead_hunter_plus_one% 1 4 9,SPCL241 5,%spl_undead_hunter_hold_immunity% 3,%spl_undead_hunter_energy_drain_immunity% 7}
		k_description:=@16
		// restore description of Smite Evil
		IF {FILE_EXISTS_IN_GAME "%spl_smite_evil_undead_hunter%.spl"}
		{{
			PATCH_WITH_TRA "%scs_tra_loc%/english/class_race.tra" "%scs_tra_loc%/%LANGUAGE%/class_race.tra" BEGIN
				new:=@717
				adv:=@714
			END
			k_description=k_description.patch{REPLACE_TEXTUALLY "^%adv%" "%adv%%WNL%%new%"}
		}}
	]





END

///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////
//// BARD KITS
///////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////////////
//// Blade - delay access to stances
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_blade BEGIN

	// clab edits
	
	kit.edit[BLADE|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{%BLADE_OFFENSIVE_SPIN% %BLADE_DEFENSIVE_SPIN%}
		k.kit_grant_powers{%BLADE_OFFENSIVE_SPIN% 5 4,%BLADE_DEFENSIVE_SPIN% 3 4}
		IF {is_iwd} {{disadvantage:=@24}}
		ELSE {{disadvantage:=19}}
		desc:=@23
		k_description=desc.patch{LPF blade_desc_swap END}
	]
END

DEFINE_PATCH_FUNCTION blade_desc_swap BEGIN
	REPLACE_TEXTUALLY "^\(.*\)BARD_SONG_PLACEHOLDER\(.*\)" "\1\2%WNL%\1%disadvantage%"
END

///////////////////////////////////////////////////////////////////////////////////////
//// Jester - rebalanced song
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_jester BEGIN

	// set some strings
	
	OUTER_SET distraction_strref=RESOLVE_STR_REF (@21)
	OUTER_SET confusion_strref=14782 // seems to work in all three
	OUTER_SET slow_strref=is_iwd?11799: (is_bg2? 335 : 14668)
	OUTER_SET unconscious_strref=20438 // works in all three
	
	// build new song
	
	spl.edit[spcl751a]
	[
		m.ab.delete{s_level>1} // start with a clean slate
		m.ab_fx.delete{!(s_opcode=321 OR s_opcode=324)} // leave in place the remove-last-version and immune-if-deafened effect
		m.ab.clone{s_level=(1+entry_index)*5|number:i=4}
		// all levels - distraction
		m.ab_fx.add{s_opcode=54 s_target=2 s_parameter1=~-2~ s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=0} // distraction
		// first level - slow 10%, confusion 10%
		m.ab_fx.add{s_opcode=40 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=89 s_probability2=80|match_parent="p_level=1"} // slow
		m.ab_fx.add{s_opcode=128 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=90|match_parent="p_level=1"} // confusion
		// fifth level - slow 20%, confusion 20%
		m.ab_fx.add{s_opcode=40 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=79 s_probability2=60|match_parent="p_level=5"} // slow
		m.ab_fx.add{s_opcode=128 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=80|match_parent="p_level=5"} // confusion
		// tenth level - slow 30%, confusion 30%
		m.ab_fx.add{s_opcode=40 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=69 s_probability2=40|match_parent="p_level=10"} // slow
		m.ab_fx.add{s_opcode=128 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=70|match_parent="p_level=10"} // confusion
		// fifteenth level - slow 40%, confusion 40%, unconscious 20%
		m.ab_fx.add{s_opcode=40 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=39 s_probability2=0|match_parent="p_level=15"} // slow
		m.ab_fx.add{s_opcode=128 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=79 s_probability2=40|match_parent="p_level=15"} // confusion
		m.ab_fx.add{s_opcode=39 s_target=2 s_parameter2=1 s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=80|match_parent="p_level=15"} // sleep
		// twentieth level - slow and confusion 50%, unconscious 50%
		m.ab_fx.add{s_opcode=40 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=49 s_probability2=0|match_parent="p_level=20"} // slow
		m.ab_fx.add{s_opcode=128 s_target=2 s_duration=7 s_save_vs_spell=1 s_probability1=49 s_probability2=0|match_parent="p_level=20"} // confusion
		m.ab_fx.add{s_opcode=39 s_target=2 s_parameter2=1 s_duration=7 s_save_vs_spell=1 s_probability1=100 s_probability2=50|match_parent="p_level=20"} // sleep
		// general edit to saving throw difficulty
		m.ab_fx.alter{s_savebonus=p_level<=5?2:(p_level=10?0:~-2~)|match="s_save_vs_spell=1"}
		// add secondaries associated with effects
		// distraction
		m.ab_fx.clone{s_opcode=325|match="s_opcode=54"} // save penalty
		m.ab_fx.clone{s_opcode=0|match="s_opcode=54"} // AC penalty
		m.ab_fx.clone{s_opcode=139 s_parameter1=distraction_strref s_parameter2=0 s_duration=0 s_timing=1|match="s_opcode=54"}// 'Distracted' string
		m.ab_fx.clone{s_opcode=142 s_parameter1=0 s_parameter2=33 |match="s_opcode=54"}// 'bad luck' icon
		// slow
		m.ab_fx.clone{s_opcode=139 s_parameter1=slow_strref s_parameter2=0 s_duration=0 s_timing=1|match="s_opcode=40"} // ''Slowed'' string
		m.ab_fx.clone{s_opcode=142 s_parameter1=0 s_parameter2=41 |match="s_opcode=40"} // 'slowed' icon
		// confusion
		m.ab_fx.clone{s_opcode=215 s_parameter1=0 s_parameter2=1 s_resource:=SPCONFUS|match="s_opcode=128"} // animation
		m.ab_fx.clone{s_opcode=139 s_parameter1=confusion_strref s_parameter2=0 s_timing=1 s_duration=0|match="s_opcode=128"} // 'confused' string
		m.ab_fx.clone{s_opcode=142 s_parameter1=0 s_parameter2=3|match="s_opcode=128"} // 'confused' icon
		// unconsciousness
		m.ab_fx.clone{s_opcode=139 s_parameter1=unconscious_strref s_parameter2=0 s_duration=0 s_timing=1|match="s_opcode=39"} // ''Unconscious' string
		m.ab_fx.clone{s_opcode=142 s_parameter1=0 s_parameter2=14|match="s_opcode=39"} // 'sleep' icon
	]

	// update description
	
	ACTION_IF is_iwd BEGIN
		OUTER_SPRINT disadvantage @20
	END ELSE BEGIN
		OUTER_SPRINT disadvantage @19
	END
	OUTER_SPRINT desc @18
	OUTER_PATCH_SAVE desc "%desc%" BEGIN
		REPLACE_TEXTUALLY BARD_SONG_PLACEHOLDER "%disadvantage%"
	END
	LAF 2daq_extract STR_VAR resref="clastext" row=JESTER column=DESCSTR RET strref2=value END
	STRING_SET_EVALUATE strref2 "%desc%"


END

///////////////////////////////////////////////////////////////////////////////////////
//// Skald - rebalanced song
///////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_skald BEGIN

	// build the song
	
	spl.edit[spcl542a]
	[
		// new L5, L10 changes
		m.ab.clone{s_level=(1+entry_index)*5|number:i=2 match="s_level=1"}
		// damp down L1 changes
		m.ab_fx.alter{ s_parameter2=1| match="p_level=1 AND (s_opcode=0 OR s_opcode=73 OR s_opcode=278)"}
		// damp down L5 changes
		m.ab_fx.alter{ s_parameter2=1| match="p_level=5 AND s_opcode=278"}
	]
	// update description

	ACTION_IF is_iwd BEGIN
		OUTER_SPRINT disadvantage @20
	END ELSE BEGIN
		OUTER_SPRINT disadvantage @19
	END
	OUTER_SPRINT desc @22

	OUTER_PATCH_SAVE desc "%desc%" BEGIN
		REPLACE_TEXTUALLY "^\(.*\)BARD_SONG_PLACEHOLDER\(.*\)" "\1\2%WNL%\1%disadvantage%"
	END
	LAF 2daq_extract STR_VAR resref="clastext" row=SKALD column=DESCSTR RET strref2=value END
	STRING_SET_EVALUATE strref2 "%desc%"

END

///////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////
// Druid kits
////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////
// Shapeshifter - new low-level and high-level versions, all shapeshifts give same form
////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION rebalance_shapeshifter BEGIN

	LAF include STR_VAR file=lib_shapeshift.tph locbase=lib END

//	adopt IWDEE-style unique weapons

	ACTION_IF !is_iwd BEGIN
		spl.edit[spcl643][m.ab_fx.alter{s_resource:=werewlf1|match="~%s_resource%~ STR_EQ brbrp"}]
		spl.edit[spcl644][m.ab_fx.alter{s_resource:=werewlf2|match="~%s_resource%~ STR_EQ brbrp"}]
		itm.copy[brbrp=>werewlf1 brbrp=>werewlf2][]
	END

// new powers


	// lesser werewolf
	
	LAF make_shapeshift_spell
		INT_VAR str=18
				str_ex=76
				dex=15
				ac=4
				apr=2
				enchantment=1
				dicenumber=1
				dicesize=6
				name=33
				description=34
				timing=1
				healing=1
				grant_natural_form_spell=1
		STR_VAR weapon="%itm_shapeshifter_lesser_werewolf%"
				copy_weapon=werewlf1
				spell="%spl_shapeshifter_lesser_werewolf%"
				copy_spell=spcl643
				damagetype=slashing
				cre=werewodr
	END
	
	// normal werewolf (basically unchanged)
	
	LAF make_shapeshift_spell
		INT_VAR str=19
				dex=16
				ac=1
				apr=2
				enchantment=2
				dicenumber=1
				dicesize=6
				timing=1
				healing=1
				resistmagic=20
				grant_natural_form_spell=1
		STR_VAR weapon=werewlf1
				spell=spcl643
				copy_spell=spcl643
				damagetype=slashing
				cre=werewodr
	END
	
	// greater werewolf (again basically unchanged, but tweaked AC)

	LAF make_shapeshift_spell
		INT_VAR str=21
				dex=20
				ac="-4"
				apr=3
				enchantment=3
				dicenumber=1
				dicesize=10
				timing=1
				healing=1
				resistmagic=40
				resistacid=50
				resistfire=50
				resistcold=50
				resistelectricity=50
				description=37
				grant_natural_form_spell=1
		STR_VAR weapon=werewlf2
				spell=spcl644
				copy_spell=spcl644
				damagetype=slashing
				cre=weregrdr
	END
	
	// werewolf lord
	
	LAF make_shapeshift_spell
		INT_VAR name=35
				description=36
				str=23
				dex=20
				ac="-8"
				apr=3
				enchantment=4
				dicenumber=1
				dicesize=12
				timing=1
				healing=1
				resistmagic=50
				resistacid=50
				resistfire=50
				resistcold=50
				resistelectricity=50
				grant_natural_form_spell=1
		STR_VAR weapon="%itm_shapeshifter_werewolf_lord%"
				spell="%spl_shapeshifter_werewolf_lord%"
				copy_spell=spcl644
				damagetype=slashing
				cre=weregrdr
	END	

// applicator spells

	spl.make[%spl_shapeshifter_werewolf_applicator%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=172 s_target=2 s_timing=1 s_resource:="%spl_shapeshifter_lesser_werewolf%"}
		m.ab_fx.add{s_opcode=171 s_target=2 s_timing=1 s_resource:=spcl643|number:i=3}
	]

	spl.make[%spl_shapeshifter_greater_werewolf_applicator%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=172 s_target=2 s_timing=1 s_resource:=spcl643}
		m.ab_fx.add{s_opcode=171 s_target=2 s_timing=1 s_resource:=spcl644|number:i=5}
	]

	spl.make[%spl_shapeshifter_werewolf_lord_applicator%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=172 s_target=2 s_timing=1 s_resource:=spcl644}
		m.ab_fx.add{s_opcode=171 s_target=2 s_timing=1 s_resource:="%spl_shapeshifter_werewolf_lord%"|number:i=7}
	]

// clab/description edits

	kit.edit[SHAPESHIFTER|edit_strrefs_in_place:i=1]
	[
		k.kit_remove_powers{spcl643 spcl644}
		k.kit_grant_powers{%spl_shapeshifter_lesser_werewolf% 1 3 4,spcl643 10,spcl644 16,%spl_shapeshifter_werewolf_lord% 22 3}
		k.kit_apply_powers{%spl_shapeshifter_werewolf_applicator% 7,%spl_shapeshifter_greater_werewolf_applicator% 13,%spl_shapeshifter_werewolf_lord_applicator% 19}
		k_description:=@32
		k_description=k_description.patch{
			shift_desc:=@58
			REPLACE_TEXTUALLY "SHAPESHIFTER_PLACEHOLDER" "%shift_desc%"
		}
	]

END

////////////////////////////////////////////////////////////////////////////////////////
// Totemic druid - extend spirit animals to L16
////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_totemic_druid BEGIN
	
	cre.copy[SPBEAR5=>SPBEAR6 SPLION5=>SPLION6 SPSNAK5=>SPSNAK6 SPWOLF5=>SPWOLF6]
	[
		m_level1=13
		m_ac_effective+="-2"
		m_thac0+="-3"
		m_save_vs_death=5
		m_save_vs_wands=7
		m_save_vs_polymorph=6
		m_save_vs_breath=5
		m_save_vs_spells=8
		m_resist_magic=20
		PATCH_MATCH "%sfo_filename%" WITH
		spbear6 BEGIN
			m_hp_max+=24
			m_hp_current+=24
			m_str=19
			m_str_ex=0
		END
		splion6 BEGIN
			m_hp_max+=18
			m_hp_current+=18
			m_str=20
			m_str_ex=0
		END
		spsnak6 BEGIN
			m_hp_max+=11
			m_hp_current+=11
			m_str=18
			m_str_ex=76
		END
		spwolf6 BEGIN
			m_hp_max+=14
			m_hp_current+=14
			m_str=18
			m_dex=21
		END
		DEFAULT
			PATCH_FAIL "shouldn't happen :%sfo_filename%"
		END
	]
	cre.copy[SPBEAR5=>SPBEAR7 SPLION5=>SPLION7 SPSNAK5=>SPSNAK7 SPWOLF5=>SPWOLF7]
	[
		m_level1=16
		m_ac_effective+="-4"
		m_thac0+="-6"
		m_attacks+=1
		m_save_vs_death=4
		m_save_vs_wands=6
		m_save_vs_polymorph=5
		m_save_vs_breath=4
		m_save_vs_spells=7
		m_resist_magic=40
		PATCH_MATCH "%sfo_filename%" WITH
		spbear7 BEGIN
			m_hp_max+=48
			m_hp_current+=48
			m_str=20
			m_str_ex=0
		END
		splion7 BEGIN
			m_hp_max+=36
			m_hp_current+=36
			m_str=21
			m_str_ex=0
		END
		spsnak7 BEGIN
			m_hp_max+=22
			m_hp_current+=22
			m_str=18
			m_str_ex=100
		END
		spwolf7 BEGIN
			m_hp_max+=28
			m_hp_current+=28
			m_str=18
			m_str_ex=76
			m_dex=22
		END
		DEFAULT
			PATCH_FAIL "shouldn't happen (%sfo_filename%)"
		END

	]

	eff.copy[SPIRSUA5=>SPIRSUA6 SPIRSUB5=>SPIRSUB6 SPIRSUC5=>SPIRSUC6 SPIRSUD5=>SPIRSUD6]
	[
		INNER_PATCH_SAVE m_resource "%m_resource%" BEGIN
			REPLACE_TEXTUALLY 5 6
		END
	]
	eff.copy[SPIRSUA5=>SPIRSUA7 SPIRSUB5=>SPIRSUB7 SPIRSUC5=>SPIRSUC7 SPIRSUD5=>SPIRSUD7]
	[
		INNER_PATCH_SAVE m_resource "%m_resource%" BEGIN
			REPLACE_TEXTUALLY 5 7
		END
	]
	
	spl.edit[spcl622 spcl623 spcl624 spcl625]
	[
		m.ab.clone{s_level=13+(entry_index*3)|match="s_level=10" number:i=2}
		m.ab_fx.alter{INNER_PATCH_SAVE s_resource "%s_resource%" BEGIN REPLACE_TEXTUALLY 5 6 END|match="p_level=13"}
		m.ab_fx.alter{INNER_PATCH_SAVE s_resource "%s_resource%" BEGIN REPLACE_TEXTUALLY 5 7 END|match="p_level=16"}
		PATCH_MATCH "%sfo_filename%" WITH
		spcl622 BEGIN
			m_description:=@108
		END
		spcl623 BEGIN
			m_description:=@109
		END
		spcl624 BEGIN
			m_description:=@110
		END
		spcl625 BEGIN
			m_description:=@111
		END		
		DEFAULT
			PATCH_FAIL "shouldn't happen"
		END
	]
	
	
	LAF 2daq_extract STR_VAR resref="clastext" row=TOTEMIC_DRUID column=DESCSTR RET strref=value END
	OUTER_SPRINT desc @112
	STRING_SET_EVALUATE strref "%desc%"










END

///////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////
// Thief kits
////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////
// Assassin - IWD version gets BG2 poison (unless already in 2.6), backstab advances 1/3 levels
////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION rebalance_assassin BEGIN

	// on IWD, implement BG style poison weapon
	
	ACTION_IF is_iwd BEGIN
		spl.copy[spcl423|source_locbase="shared/bg_poison"]
		[
			s_name=36031
			s_description:=@56
		]
		LAF install STR_VAR files="bdpweapn.eff bdpweapn.spl" locbase="shared/bg_poison" END
	END

	// implement backstab changes

	COPY_EXISTING backstab.2da override
		LPF 2da_read RET_ARRAY stab_array=array levels=columns END
		PHP_EACH levels AS level=>discard BEGIN
			LPF assassin_backstab INT_VAR level RET value END
			SET $stab_array("ASSASIN" "%level%")=value
		END
		LPF 2da_write STR_VAR array=stab_array default="0" END
	BUT_ONLY


	// update class description
	OUTER_SPRINT old_bs_desc @50
	OUTER_SPRINT new_bs_desc @51
	OUTER_SPRINT discard1 @52
	OUTER_SPRINT discard2 @53
	OUTER_SPRINT poison_starter @54
	OUTER_SPRINT poison_main @55
	
	LAF 2daq_extract STR_VAR resref="clastext" row=ASSASSIN column=DESCSTR RET strref2=value END
	ACTION_GET_STRREF strref2 desc
	OUTER_PATCH_SAVE desc "%desc%" BEGIN
		REPLACE_TEXTUALLY ".*%discard1%.*[%WNL%%MNL%%LNL%]" ""
		REPLACE_TEXTUALLY ".*%discard2%.*[%WNL%%MNL%%LNL%]" ""
		REPLACE_TEXTUALLY "%old_bs_desc%" "%new_bs_desc%"
		PATCH_IF is_iwd BEGIN
			REPLACE_TEXTUALLY "%poison_starter%.*" "%poison_starter% %poison_main%" 
		END
	END
	STRING_SET_EVALUATE strref2 "%desc%"



END

DEFINE_PATCH_FUNCTION assassin_backstab INT_VAR level=0 RET value BEGIN

	PATCH_MATCH "%level%" WITH
	0 BEGIN
		value=1
	END
	1 2 3 BEGIN
		value=2
	END
	4 5 6 BEGIN
		value=3
	END
	7 8 9 10 BEGIN
		value=4
	END	
	11 12 13 14 BEGIN
		value=5
	END
	15 16 17 18 BEGIN
		value=6
	END
	DEFAULT
		value=7
	END
	
	
	

END
