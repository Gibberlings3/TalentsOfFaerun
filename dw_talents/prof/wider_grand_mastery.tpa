DEFINE_ACTION_FUNCTION wider_grand_mastery STR_VAR version=0 BEGIN

	// 0: just MC fighters
	// 1: just rangers/paladins
	// 2: paladins and multiclass warriors

	LAF include STR_VAR file=prof_tools.tph END
	LAF include STR_VAR file=profdoc.tph END
	
	// main code
	
	LAF check_ini STR_VAR ini=mc_fighter_mastery RET mc_fighter_mastery=value END
	LAF check_ini STR_VAR ini=paladin_mastery RET paladin_mastery=value END
	LAF check_ini STR_VAR ini=ranger_mastery RET ranger_mastery=value END
	LAF check_ini STR_VAR ini=mc_ranger_mastery RET mc_ranger_mastery=value END

	ACTION_MATCH "%version%" WITH
	0 BEGIN
		OUTER_SPRINT class "FIGHTER_MAGE FIGHTER_CLERIC FIGHTER_DRUID FIGHTER_THIEF FIGHTER_MAGE_CLERIC FIGHTER_MAGE_THIEF"
		OUTER_SPRINT kit ""
	END
	1 BEGIN
		OUTER_SPRINT class "PALADIN RANGER"
		OUTER_SPRINT kit ""
	END
	2 BEGIN
		OUTER_SPRINT class "FIGHTER_MAGE FIGHTER_CLERIC FIGHTER_DRUID FIGHTER_THIEF FIGHTER_MAGE_CLERIC FIGHTER_MAGE_THIEF PALADIN RANGER CLERIC_RANGER"
		OUTER_SPRINT kit "DW_DRUID_RANGER DW_NIGHT_WOLF"
	END
	DEFAULT
		FAIL "unrecognized version %version% for wider_grand_mastery" 
	END
	LAF alter_profs STR_VAR kit class function=grand_mastery_helper END

	// manual tweaks
	
	// Dwarven Defender multiclasses deserve 4 pips in axe/hammer, 2 in everything else

	LAF alter_profs STR_VAR kit="DW_ABJURANT_DEFENDER DW_MORADIN_HAMMER" function=dd_patch END

	// reexternalize

	ACTION_IF FILE_EXISTS "%data_loc_shared%/weapprof.2da" BEGIN
		LAF externalize_weapprof END
	END
	
	// Regenerate descriptions

	LAF update_all_proficiency_descriptions INT_VAR races=0 STR_VAR class_filter=warrior_filter END








END

DEFINE_PATCH_FUNCTION grand_mastery_helper 
	INT_VAR prof_id=0
			value=0
	STR_VAR class=""
	RET value_new
BEGIN
	PATCH_IF ((prof_id>=89 && prof_id<=110) || prof_id=115) BEGIN
		PATCH_IF value=2 BEGIN
			PATCH_MATCH "%class%" WITH
			PALADIN BEGIN
				value_new=paladin_mastery
			END
			RANGER BEGIN
				value_new=ranger_mastery
			END
			CLERIC_RANGER DRUID_RANGER BEGIN
				value_new=mc_ranger_mastery
			END
			DEFAULT
				value_new=mc_fighter_mastery
			END			
		END ELSE BEGIN
			value_new=value
		END
	END ELSE BEGIN
		value_new=value
	END
END

DEFINE_PATCH_FUNCTION dd_patch 
	INT_VAR prof_id=0
			value=0
	STR_VAR class=""
	RET value_new
BEGIN
	value_new=value
	PATCH_IF prof_id>=89 && (prof_id<111 || prof_id=115) BEGIN
		PATCH_MATCH "%prof_id%" WITH 
		92 97 BEGIN
			value_new = 4
		END
		DEFAULT
			value_new = 2
		END
	END
END

DEFINE_PATCH_FUNCTION warrior_filter
	STR_VAR parent_class=""
	RET proceed
BEGIN
	PATCH_MATCH "%parent_class%" WITH
	".*FIGHTER.*" ".*RANGER" PALADIN BEGIN
		proceed=1
	END
	DEFAULT
		proceed=0
	END
END
			
