DEFINE_ACTION_FUNCTION faerie_fire BEGIN

 LAF check_label STR_VAR label=dw#faerie_fire RET value END
 ACTION_IF !value BEGIN
 
   LAF make_label STR_VAR label=dw#faerie_fire END
	
  WITH_TRA "%scs_tra_loc%/english/faerie_fire.tra" "%scs_tra_loc%/%LANGUAGE%/faerie_fire.tra" BEGIN


	// get icon entry

	OUTER_SPRINT string @701
	LAF resolve_statdesc STR_VAR string bam="dwfaeico" locbase="shared_som/icon" RET stat_num END	

	// get splprot to block FF outdoors
	
	LAF resolve_splprot_entry INT_VAR stat=0x106 value=2 relation_number=11 STR_VAR description="NOT_DAYNIGHT" RET outdoor_value=value END // when area has day/night
	LAF resolve_splprot_entry INT_VAR stat=0x104 value=outdoor_value relation_number=90 STR_VAR description="OUTDOOR_AND_LIGHT" RET splprot_value=value END // when area has day/night and it's daytime

		
	// make base spell (innate version)
	
	spl.copy[sppr116=>faerie_fire|path="%workspace%" source_path="%MOD_FOLDER%/shared_som/resource"]
	[
		m_name:=@701
		m_description:=@702
		m_icon:=DWFAEC
		m_sectype=0 // non-hostile (could be disputed)
		m_type:=innate
		// no blocks above level 20 after the Time of Troubles
		IF {!is_iwd} {{
			m.ab.delete{s_level>20}
		}}
		// set header
		m.ab.alter{s_projectile=0 s_location=4 s_casting_time=1 s_icon:=DWFAEB}
		// remove cruft and undocumented demon/undead immunity
		m.ab_fx.delete{s_opcode=206 OR s_opcode=318}
		// visual effect on target
		m.ab_fx.alter{s_target=2|match="s_opcode=215"}
		// set string correctly
		m.ab_fx.alter{s_parameter1=RESOLVE_STR_REF(@701) | match="s_opcode=139"}
		// set icon correctly
		m.ab_fx.alter{s_parameter2=stat_num|match="s_opcode=142"}
		// remove previous effects
		m.ab_fx.add{s_opcode=321 s_timing=1 s_target=2 s_resource:=faer_plc|insert_point:i=0}
		// handle daylight
		m.ab_fx.add{s_opcode=318 s_target=2 s_duration=1 s_parameter2=splprot_value s_resource:=faer_plc|insert_point:i=0}
		m.ab_fx.add{s_opcode=326 s_target=2 s_duration=0 s_timing=1 s_parameter2=splprot_value s_resource:="%spl_faerie_fire_secondary%"|insert_point:i=0}
		
	]
	/*
	COPY "%MOD_FOLDER%/shared_som/resource/sppr116.spl" "%workspace%/faerie_fire.spl" 
		SAY 0x8 @701
		SAY 0x50 @702
		WRITE_LONG 0x18 0 // non-hostile (could be disputed)
		WRITE_SHORT 0x1c 4
		WRITE_ASCII 0x3a DWFAEC (8)
		LPF ALTER_SPELL_HEADER INT_VAR projectile=1 location=4 speed=1 STR_VAR icon=dwfaeb END
		LPF ALTER_EFFECT INT_VAR match_opcode=215 target=2 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@701) END
		LPF DELETE_EFFECT INT_VAR match_opcode=318 END // remove undocumented undead/demon protection
		LPF DELETE_EFFECT INT_VAR match_opcode=206 END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 timing=1 target=2 STR_VAR resource="faer_plc" END
		LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=stat_num END
		// this handles daylight		
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=318 target=2 timing=0 duration=1 parameter1=0 parameter2=splprot_value STR_VAR resource=faer_plc END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=326 target=2 timing=0 duration=1 parameter1=0 parameter2=splprot_value STR_VAR resource="%spl_faerie_fire_secondary%" END
	*/
	
	// install innate spell
	
	COPY "%workspace%/faerie_fire.spl" "override/%spl_faerie_fire%.spl"
		REPLACE_TEXTUALLY faer_plc "%spl_faerie_fire%" (8)
		
	// if appropriate, install priest spell
	
	ACTION_IF !VARIABLE_IS_SET CLERIC_FAERIE_FIRE BEGIN
		COPY "%workspace%/faerie_fire.spl" "%workspace%/faerie_fire_priest.spl"
			WRITE_SHORT 0x1c 2
			LPF ALTER_SPELL_HEADER INT_VAR location=2 speed=4 END
		LAF extended_add_spell STR_VAR id=CLERIC_FAERIE_FIRE file="faerie_fire_priest" path="%workspace%" RET CLERIC_FAERIE_FIRE=resref END
		APPEND "hidespl.2da" "%CLERIC_FAERIE_FIRE% 1 0"
		LAF spl_make_scroll STR_VAR spell_resref="%CLERIC_FAERIE_FIRE%" END
	END ELSE BEGIN
		
		// if not, make sure it doesn't work in daylight
		COPY_EXISTING "%CLERIC_FAERIE_FIRE%.spl" override
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=318 target=2 timing=0 duration=1 parameter1=0 parameter2=splprot_value STR_VAR resource=faer_plc END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=326 target=2 timing=0 duration=1 parameter1=0 parameter2=splprot_value STR_VAR resource="%spl_faerie_fire_secondary%" END
		
	END
	
	// either way, handle 318/321
	
	COPY_EXISTING "%CLERIC_FAERIE_FIRE%.spl" override
		REPLACE_TEXTUALLY faer_plc "%spl_faerie_fire%" (8)
	
	// bring across icons
	
	LAF install STR_VAR file="dwfaec.bam dwfaeb.bam dwfaeico.bam" locbase="shared_som/icon" END
		
	// block FF outdoors message
	
	CREATE spl "%spl_faerie_fire_secondary%" 
		LPF add_basic_spell_ability END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=139 target=2 timing=1 parameter1=RESOLVE_STR_REF(@705) END
	
	
	// make sure the versions play nice with each other
		
	COPY_EXISTING "%spl_faerie_fire%.spl" override
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=2 opcode=321 timing=1 target=2  STR_VAR resource="%CLERIC_FAERIE_FIRE%" END
	BUT_ONLY

	COPY_EXISTING "%CLERIC_FAERIE_FIRE%.spl" override
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=2 opcode=321 timing=1 target=2  STR_VAR resource="%spl_faerie_fire%" END
	BUT_ONLY


	ACTION_IF GAME_INCLUDES sod BEGIN

	
		COPY_EXISTING "%CLERIC_FAERIE_FIRE%.spl" override
			LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=stat_num END
		BUT_ONLY
		
	
	END




  END
  END






END