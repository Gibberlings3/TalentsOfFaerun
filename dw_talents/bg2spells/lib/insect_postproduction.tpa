DEFINE_ACTION_FUNCTION insect_postproduction BEGIN

		// get immunity string from Summon Insects
		
		ACTION_GET_STRREF 36302 old_desc
		OUTER_PATCH "%old_desc%" BEGIN
			REPLACE_EVALUATE "^\([A-Z].*\)$" BEGIN
				SPRINT extra "%MATCH1%"
			END
			"%extra%"
		END

		// add immunity to undead/constructs/elementals; update description
		
		ACTION_FOR_EACH spell IN "%CLERIC_CREEPING_DOOM%" "%CLERIC_INSECT_PLAGUE%" BEGIN
			COPY_EXISTING "%spell%.spl" override
				READ_LONG 0x50 desc_str
				GET_STRREF desc_str desc
				INNER_PATCH_SAVE desc "%desc%" BEGIN
					LPF insect_desc STR_VAR extra END
				END
				INNER_ACTION BEGIN
					STRING_SET_EVALUATE desc_str "%desc%"
				END
				LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=324 target=2 power=0 parameter2=55 duration=1 STR_VAR resource="%spell%" END
				LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=324 target=2 power=0 parameter2=9 duration=1 STR_VAR resource="%spell%" END
		END

END

DEFINE_PATCH_FUNCTION insect_desc STR_VAR extra="" BEGIN

	end=BUFFER_LENGTH
	INSERT_BYTES end (STRING_LENGTH "%WNL%%WNL%%extra%")
	WRITE_ASCIIE end "%WNL%%WNL%%extra%"

END