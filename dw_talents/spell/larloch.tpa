DEFINE_ACTION_FUNCTION larloch BEGIN

// various mods alter larloch, let's do a crude check for them: any extra abilities?

	COPY_EXISTING "%WIZARD_LARLOCH_MINOR_DRAIN%.spl" override
		proceed= ((SHORT_AT 0x68)<2)
	BUT_ONLY
	
	// main patch
	
	ACTION_IF proceed BEGIN
	
		ACTION_IF enhanced_edition BEGIN
			spl.edit[%WIZARD_LARLOCH_MINOR_DRAIN% %INNATE_LARLOCHS_MINOR_DRAIN%|allow_missing:i=1 edit_strrefs_in_place:i=1]
			[
				m_description:=@27
				m.ab_fx.alter{s_parameter1=0 s_parameter2=1|match="s_opcode=146"}
			]

			spl.edit[%WIZARD_LARLOCH_MINOR_DRAIN%A %INNATE_LARLOCHS_MINOR_DRAIN%A|allow_missing:i=1 edit_strrefs_in_place:i=1]
			[
				m.ab.clone{s_level=3+2*entry_index|number:i=4}
				m.ab_fx.alter{s_dicenumber=p_level/2 + 1 s_dicesize=4 s_drain_hp_to_caster=1|match="s_opcode=12"}
			]
		END ELSE BEGIN
			spl.edit[%WIZARD_LARLOCH_MINOR_DRAIN% %INNATE_LARLOCHS_MINOR_DRAIN%|allow_missing:i=1 edit_strrefs_in_place:i=1]
			[
				m_description:=@27
				m.ab.clone{s_level=3+2*entry_index|number:i=4}
				m.ab_fx.delete{s_opcode=146}
				m.ab_fx.add{s_opcode=12 s_target=2 s_timing=1 s_parameter2b=64 s_dicenumber=p_level/2 + 1 s_dicesize=4}
				m.ab_fx.add{s_opcode=18 s_target=1 s_timing=0 s_duration=60 s_parameter1=(p_level/2 + 1)*3}	
			]
		
		END
		
		// on BG2, make level 1 copy for Foebane, and implement
		
		ACTION_IF is_bg2 && enhanced_edition BEGIN
			spl.copy[%INNATE_LARLOCHS_MINOR_DRAIN%A=>%spl_larloch_foebane%]
			[
				m.ab.delete{s_level>1}
			]
			
			ACTION_FOR_EACH foebane IN sw1h63 comps63 BEGIN
				COPY_EXISTING "%foebane%.itm" override
					REPLACE_TEXTUALLY "%INNATE_LARLOCHS_MINOR_DRAIN%A" "%spl_larloch_foebane%" (8)
			END
		
		END
	
	END

END