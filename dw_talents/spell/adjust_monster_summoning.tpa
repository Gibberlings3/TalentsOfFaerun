DEFINE_ACTION_FUNCTION adjust_monster_summoning BEGIN

	LAM data_saving_throws
	LAM data_thac0

	// on IWD, install original rebalances
	
	ACTION_IF is_iwd BEGIN
		LAF run STR_VAR file=monster_summoning_iwd END
	END
	
	// sanity check: are my 
	
	// number boosts
	
	OUTER_FOR (level=2;level<=7;++level) BEGIN
		OUTER_SPRINT spell EVAL "%WIZARD_MONSTER_SUMMONING_%level%%"
		spl.edit[%spell%|edit_strrefs_in_place:i=1]
		[
			old_tra=600+level*10
			new_tra=old_tra+1
			old:=(AT old_tra)
			new:=(AT new_tra)
			INNER_PATCH_SAVE m_description "%m_description%" BEGIN
				REPLACE_TEXTUALLY "%old%" "%new%"
			END
		]
		ACTION_IF FILE_EXISTS_IN_GAME "%spell%a.spl" BEGIN
			OUTER_SPRINT spell "%spell%a"
		END
		spl.edit[%spell%]
		[
			m.ab_fx.alter{s_dicenumber=2 s_dicesize=3|match="s_opcode=331"}
		]
	END
	
	//MS7 (L11-12)
	

	// Ettin increased to L12
	
	cre.edit[DW#MS7ET]
	[
		m_level1=12
		m.cre_enforce_saves{}
	]
	
	// Umber Hulk increased to L11 and given better stats

	cre.edit[MS7UMBH]
	[
		m_level1=12
		m.cre_enforce_saves{}
		m_hp=80
		m_str=19
		m_con=18
	]
	
	//MS 6 (L9-10)
	
	// salamanders raised to L9
	
	cre.edit[ms6salc ms6salf]
	[
		m_level1=9
		m.cre_enforce_saves{}
		m.cre_enforce_thac0{}
		m_str=17
		m_dex=16
		m_con=14
		m_hp=72
	]
	
	// giant troll raised to L10
	
	cre.edit[dw#ms6gt]
	[
		m_level1=10
		m.cre_enforce_saves{}
		m.cre_enforce_thac0{}
	]
	
	// add a yuan-ti elite
	
	cre.copy[icyuan04=>dw#ms6yt|source_location=resource]
	[
			m_both_names:=@601
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms6yuant
			m_no_corpse=1
			m_xp_value=0
			m.cre_swap_scripts{wtasight=>bdsum00}
			m.item.alter{s_unstealable=1 s_undroppable=1}
	]
	
	LAF spl_make_summoning_2da STR_VAR monsters="MS6SALC MS6SALF DW#MS6GT DW#MS6YT" filename=msummo6 END
	
	//MS5 (L7-8)
	
	// minotaur raised to L7
	
	cre.edit[MS5MINO]
	[
		m_level1=7
		m.cre_enforce_saves{}
		m.cre_enforce_thac0{}
	]
	
	// add a greater yuan-ti
	
	cre.copy[icyuan02=>dw#ms5yt|source_location=resource]
	[
			m_both_names:=@600
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms5yuant
			m_no_corpse=1
			m_xp_value=0
			m.cre_swap_scripts{wtasight=>bdsum00}
			m.item.alter{s_unstealable=1 s_undroppable=1}
	]
	
	LAF spl_make_summoning_2da STR_VAR monsters="MS5MINO DW#MS5TR DW#MS5YT" filename=msummo5 END
	
	
	//MS4 (L6)
	
	//drop L4 yuan-ti to 6th level
	
	cre.edit[dw#ms4yu]
	[
		m_level1=6
		m.cre_enforce_saves{}
	]
	
	//add ogre berserker, moved to L6
	
	cre.edit[ogregrsu]
	[
		m_level1:=6
		m.cre_enforce_saves{}
	]
	
	LAF spl_make_summoning_2da STR_VAR monsters="OGREMASU OGREGRSU DW#MS4YU" filename=msummo4 END

	// MS3 (L4-5)
	
	//add ogre, remove ogre berserker
	
	LAF spl_make_summoning_2da STR_VAR monsters="OGRESU ETTERCSU HOBGOBSU" filename=msummo3 END
	
	// MS2 (L3)
	
	// remove ogre
	
	LAF spl_make_summoning_2da STR_VAR monsters="MS2LIZM OGRELESU GNOLLSU " filename=msummo2 END
	
	// MS1 (L1)
	
	// stet

END