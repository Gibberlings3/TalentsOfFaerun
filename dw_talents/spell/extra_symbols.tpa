DEFINE_ACTION_FUNCTION extra_symbols BEGIN

 ACTION_IF VARIABLE_IS_SET CLERIC_SYMBOL_PAIN && VARIABLE_IS_SET CLERIC_SYMBOL_HOPELESSNESS BEGIN

	// make the spells
	ACTION_FOR_EACH symbol IN CLERIC_SYMBOL_PAIN CLERIC_SYMBOL_HOPELESSNESS BEGIN
			OUTER_PATCH_SAVE idsname "%symbol%" BEGIN
				REPLACE_TEXTUALLY "CLERIC" "WIZARD"
			END
			OUTER_SPRINT spell EVAL "%%symbol%%"
			newname,newname_scroll=spl.copy[%spell%=>%idsname%|standard_icons:i=0]
			[
				m_level=8
				m_type:=wizard
				m_unusable_druid=0
				m.spl_enforce_school{}
				m.ab.alter{s_casting_time=8}
				m.ab_fx.alter{s_power=s_power=7?8:s_power}
				m_description=m_description.patch{LPF patch_symbol_desc_to_wizard END}
			]
			spl.edit[%newname% %spell%]
			[
				m.ab_fx.clone{s_resource:=%newname%|match="s_opcode=321"}
			]
			OUTER_SPRINT "%idsname%" "%newname%"
			OUTER_SPRINT "%idsname%_SCROLL" "%newname_scroll%"
	END
	
	// install the scrolls
	
	OUTER_SPRINT shadow_pain scrlao
	OUTER_SPRINT shadow_hopelessness scrlao
	COPY_EXISTING_REGEXP ".*\.sto" override
		PATCH_IF INDEX_BUFFER ("\(%shadow_pain%\|%shadow_hopelessness%\)")>=0 BEGIN
			SPRINT $stores_to_edit("%SOURCE_RES%") ""
		END
	BUT_ONLY

	ACTION_PHP_EACH stores_to_edit AS store=>null BEGIN
		sto.edit[%store%]
		[
			m.item.clone{s_resref:="%WIZARD_SYMBOL_PAIN_SCROLL%"|match="~%s_resref%~==~%shadow_pain%~"}
			m.item.clone{s_resref:="%WIZARD_SYMBOL_HOPELESSNESS_SCROLL%"|match="~%s_resref%~==~%shadow_hopelessness%~"}
		]
	END
	
	END

END

DEFINE_PATCH_FUNCTION patch_symbol_desc_to_wizard
BEGIN
	SPRINT level @38
	REPLACE_TEXTUALLY "%level% *7" "%level% 8"
	SPRINT sphere @37
	REPLACE_TEXTUALLY "%sphere%.*[%WNL%%LNL%%MNL%]" ""
	SPRINT casting_time @32
	REPLACE_TEXTUALLY "%casting_time% *3" "%casting_time% 8"
	SPRINT priest @30
	SPRINT wizard @31
	REPLACE_TEXTUALLY "%priest%" "%wizard%"
END
