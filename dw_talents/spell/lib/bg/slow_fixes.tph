
DEFINE_ACTION_FUNCTION slow_fixes BEGIN

	ACTION_IF !FILE_EXISTS "weidu_external/markers/dw-slow_fixes.mrk" BEGIN // idempotence
	
		COPY_EXISTING "misc01.itm" "weidu_external/markers/dw-slow_fixes.mrk"

	/*
		The main applications of Slow impose -4 AC and THAC0. That's what 'slowed' means in PnP D&D, and I think also in BG2, which
		uses the 'slowed' descriptor without defining it other than via the Slow spell. (And the negative effects of Slow are because
		of being slowed, not additional to it.) Many effects actually lack it but I think
		this is for technical reasons: avoiding cumulative effects is a complete pain for slow. (note that Ardulia's Fall does have it, 
		but is not protected from cumulativity.)
		
		But, since we're up for removing the pain, we're going to go with it. Handling cumulativity will be done elsewhere.
	*/
		
		// add missing THAC0/AC effects
		
		ACTION_FOR_EACH resource IN
			ACIDOOZ3.ITM // ooze attack
			AURSTAF.ITM // Staff of the Ram
			BALTH07.SPL // Balth malison
			BDSHA06B.SPL // spirit attack
			BDSW2H01.SPL // Dragon blade payload
			BHAAL2B.SPL // Bhaal dark taint
			BLUN14.ITM // Flail of Ages
			BLUN30.ITM // Flail of Ages
			BLUN30C.ITM // Flail of Ages
			BLUN30D.ITM // Flail of Ages
			BONEDAG.ITM // Bone Blade weapon
			DOGWAWP.ITM // Astral Hound bite
			DWWHIP.ITM  // drow whip of fangs
			DWWHIP01.ITM // drow whip of fangs
			GORWOM4.ITM // drow whip of fangs
			HGBER01.ITM // Berenn sword
			JELLMU1.ITM // Jelly attack
			JELLMU2.ITM // Jelly attack
			KUOSPER.ITM // Kuo-toa spear
			OHBDGELU.ITM // Gelugon spear
			OHBEWWSL.SPL // OH slow effect
			OHHPHREY.ITM // Soultaker dagger
			OHNPOTN1.ITM // mysterious green dragon potion
			MELIS01.SPL // Melissan's bone blades
			PLYJELLO.ITM // Jelly attack
			PLYJELLY.ITM // jelly attack
			POTN32.ITM // Cursed potion
			SENDAI.ITM // drow whip of fangs
			SHARSWD.ITM // Xei Win Toh's weapon
			SPCL415.SPL // trap
			SPCL751A.SPL // jester effect
			SPERMEL.ITM // Melissan's attack
			SPPR250.SPL // Writhing fog
			SPWM112.SPL // wild magic effect
			TROLLTOR.ITM // Torgal
		BEGIN
			COPY_EXISTING "%resource%" override
			// clear out previous versions (would not be needed on a FP run)
				LPF DELETE_EFFECT INT_VAR match_opcode=0 match_parameter1="-4" END
				LPF DELETE_EFFECT INT_VAR match_opcode=54 match_parameter1="-4" END
			// apply new versions				
				LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=40 opcode=54 parameter1="-4" END
			IF_EXISTS
		END

		// holy word needs a subspell
		
		CREATE spl DW-SLOW
			INSERT_BYTES 0x72 0x28
			WRITE_BYTE 0x72+0xc 2 // target
			WRITE_SHORT 0x72+0x10 1 // min level
			WRITE_SHORT 0x68 1
			WRITE_LONG 0x6a (0x28+THIS)
			LPF ADD_SPELL_EFFECT INT_VAR opcode=40 target=2 parameter1="-4" duration=60 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=0 target=2 parameter1="-4" duration=60 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=54 target=2 parameter1="-4" duration=60 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=139 target=2 timing=1 duration=0 parameter1=14668 parameter2=0 END // 'slowed' string

		COPY_EXISTING "slow.eff" override
			WRITE_LONG 0x10 146  // cast spell
			WRITE_LONG 0x1c 1	 // parameter1=1
			WRITE_LONG 0x20 1	 // parameter2=1
			WRITE_LONG 0x28 0	 // duration=0
			WRITE_LONG 0x24 1	 // timing=1
			WRITE_ASCII 0x30 "DW-SLOW" // payload

		
	END
	
END