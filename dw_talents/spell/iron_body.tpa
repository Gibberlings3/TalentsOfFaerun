DEFINE_ACTION_FUNCTION iron_body BEGIN

	ACTION_IF VARIABLE_IS_SET "WIZARD_IRON_BODY" BEGIN
	
		// main spell
		
		spl.edit[%WIZARD_IRON_BODY%|edit_strrefs_in_place:i=1]
		[
			// physical damage resistance
			m.ab_fx.alter{s_parameter1=75|match="s_opcode=87"}
			m.ab_fx.clone{s_opcode=entry_index=0?86:(entry_index=1?88:89)|number:i=3 match="s_opcode=87"}
			// acid resistance
			m.ab_fx.clone{s_opcode=27|match="s_opcode=30"}
			// remove spell failure
			m.ab_fx.delete{s_opcode=145}
			// immunity to further physical damage adjusts
			m.ab_fx.clone{s_parameter2=86+entry_index|number:i=4 match="s_opcode=101 AND s_parameter2=78"}
			// spellcasting slowdown
			m.ab_fx.clone{s_opcode=189 s_parameter1="-2" s_parameter2=0|match="s_opcode=142 AND s_parameter2=172"}
			// description
			m_description:=@46
		]
		
		// shapechange
		
		COPY_EXISTING "spell.ids" "%workspace%"
			REPLACE_EVALUATE "\([A-Z0-9_]+\(SHAPESHIFT\|SHAPECHANGE\|POLYMORPH\)[A-Z0-9_]+\)" BEGIN
				SPRINT spell "%MATCH1%"
				PATCH_IF INDEX ("NATURAL" "%spell%") <0 BEGIN
					PATCH_IF VARIABLE_IS_SET "%spell%" BEGIN
						SPRINT resref EVAL "%%spell%%"
						PATCH_IF FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
						  INNER_ACTION BEGIN	
							COPY_EXISTING "%resref%.spl" override
								LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 timing=1 STR_VAR resource="%WIZARD_IRON_BODY%" END
						  END
						END
					END
				END
			END
			""
		BUT_ONLY

	
	END

END