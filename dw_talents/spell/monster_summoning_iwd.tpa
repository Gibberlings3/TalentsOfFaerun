DEFINE_ACTION_FUNCTION monster_summoning_iwd BEGIN

	WITH_TRA "%MOD_FOLDER%/iwdspells/lang/english/dw_iwdspells.tra" "%MOD_FOLDER%/iwdspells/lang/%LANGUAGE%/dw_iwdspells.tra" BEGIN
		LAM data_saving_throws

	/////////////////////////////////////////////////////
	//// MS7
	///////////////////////////////////////////////////

		//// Umber hulk - unchanged

		//// Ettin - baseline is BG2 ettin

		cre.copy[pettin=>dw#ms7et|source_location=resource]
		[
			m_both_names:=@15
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms7ettin
			m_no_corpse=1
			m_xp_value=0
			m.cre_remove_items{b3-12}
			m.cre_add_items{b2-16m2}
			m.cre_swap_scripts{wtasight=>gnsummm}
		]

		//// main summoning

	   LAF spl_make_summoning_2da STR_VAR monsters="MS7UMBH DW#MS7ET" filename=msummo7 END

	/////////////////////////////////////////////////////
	//// MS6
	///////////////////////////////////////////////////


	/// Frost salamander  - IWDEE's MS6SALC

	//// Fire salamanader - IWDEE's MS6SALF

	//// giant troll - base is BG2 trolgi01

		cre.copy[trolgi01=>dw#ms6gt|source_location=resource]
		[
			m_both_names:=@17
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms6gttr1
			m_no_corpse=1
			m_xp_value=0
			m.cre_strip_scripts{gensht01}
			m.cre_swap_scripts{wtasight=>gnsummm}
			m.cre_remove_items{monhp1}
			m.immunity_spell{trollreg}		
		]


		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="MS6SALC MS6SALF DW#MS6GT" filename=msummo6 END

	/////////////////////////////////////////////////////
	//// MS5
	///////////////////////////////////////////////////


		/// ordinary troll
		
		cre.copy[troll01=>dw#ms5tr|source_location=resource]
		[
			m_both_names-strref=3008
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms5troll
			m_no_corpse=1
			m_xp_value=0
			m.cre_swap_scripts{wtatroll=>gnsummm}
			m.cre_remove_items{monhp1}
			m.immunity_spell{trollreg}				
		]

		//minotaur - unchanged
		
		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="MS5MINO DW#MS5TR" filename=msummo5 END

	/////////////////////////////////////////////////////
	//// MS4
	///////////////////////////////////////////////////


		// ogre mage - based on unused monster-summon in BG2

		cre.copy[ogremasu|source_location=resource]
		[
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms4ogrem
			m_no_corpse=1
			m_xp_value=0
			m.cre_remove_items{comps02}
			m.cre_add_items{comps01}
			m.cre_set_proficiencies{singleweapon=>2 bastardsword=>2}
			m.cre_remove_spells{all}
			m.cre_add_spells{MAGIC_MISSILE(2) BURNING_HANDS(2) MIRROR_IMAGE MELF_ACID_ARROW LIGHTNING_BOLT GHOST_ARMOR}
			m.cre_swap_scripts{wtasight=>gnsummm}
		]

		LAF ds_resolve_stat STR_VAR id=ITEM_MM END
		LAF ds_resolve_stat STR_VAR id=ITEM_MM_SEEN END
		LAF ds_resolve_stat STR_VAR id=WIZARD_SHIELD END
		LAF ds_resolve_stat STR_VAR id=DO_NOT_TARGET_SPELLS END 
		install[ogremasu.baf|location=resource]

		// yuan-ti

		cre.copy[icyuan01=>dw#ms4yu|source_location=resource]
		[
			m_both_names:=@16
			m_allegiance:=controlled
			m_gender:=summoned
			m_dv:=ms4yuant
			m_no_corpse=1
			m_xp_value=0
			m.cre_swap_scripts{wtasight=>gnssummm}
			m.item.alter{s_unstealable=1 s_undroppable=1}
		]
		
		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="OGREMASU DW#MS4YU" filename=msummo4 END


	/////////////////////////////////////////////////////
	//// MS3
	///////////////////////////////////////////////////

		// ogre berserker

		cre.edit[ogregrsu]
		[
			m_no_corpse=1
			m_xp_value=1
			m.cre_add_spells{BERSERKER_RAGE}
			m.cre_set_proficiencies{flailmorningstar=>2}
			m.cre_remove_items{ogre1}
			m.cre_add_items{blun06(unstealable,undroppable)}
			m.cre_insert_script{dw#msogr|loc=high}
		]
		LAF ds_resolve_stat STR_VAR id=STATE_ENRAGED RET stat_ind END
		install[dw#msogr.baf|location=resource]

		// hobgoblin elite
		
		cre.edit[hobgobsu]
		[
			m_no_corpse=1
			m_xp_value=1
			m.cre_set_proficiencies{bastardsword=>2 longbow=>2}
			m_script_default:=gnsummm
		]
		
		// ettercap

		cre.edit[ettercsu]
		[
			m_no_corpse=1
			m_xp_value=1
			m_script_default:=gnsummm
		]

		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="OGREGRSU ETTERCSU HOBGOBSU" filename=msummo3 END
		

	/////////////////////////////////////////////////////
	//// MS2
	///////////////////////////////////////////////////

		// gnoll
		
		cre.edit[gnollsu]
		[
			m_level1=3
			m_both_names-strref=6627
			m_hp=24
			m_attacks=1
			m_no_corpse=1
			m_xp_value=0
			m.cre_set_proficiencies{spear=>1}
			m_script_default:=gnsummm
		]
		
		// ogrillon
		
		cre.edit[ogrelesu]
		[
			m_attacks=1
			m_no_corpse=1
			m_xp_value=0
			m_script_default:=gnsummm
		]
		
		// lizard man - unaltered
		
		cre.edit[ms2lizm]
		[
			m_level1=3
			m_xp_value=0
			m.cre_swap_scripts{gnsummm=>bdsum00}
		]
		
		// ogre

		cre.edit[ogresu]
		[
			m_no_corpse=1
			m.cre_swap_scripts{wtasight=>gnsummm}
		]

		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="MS2LIZM OGRELESU GNOLLSU OGRESU" filename=msummo2 END

		
	/////////////////////////////////////////////////////
	//// MS1
	///////////////////////////////////////////////////

		// goblins
		
		cre.edit[ms1goba ms1gobm]
		[
			m_xp_value=0
			m.item.alter{s_unstealable=1 s_undroppable=1}
		]

		cre.copy[hobwar01=>dw#ms1hw hobarc01=>dw#ms1ha|source_location=resource]
		[
			IF {"%SOURCE_RES%" STR_EQ hobwar01} {{m_both_names:=@13}} ELSE {{m_both_names:=@14}}
			m_gender:=summoned
			m_no_corpse=1
			m_class:=hobgoblin
			m_allegiance:=controlled
			m_xp_value=0
			m_dv:=ms1hobgo
			m.cre_strip_scripts{gensht01 hgarch01 hgwarr01}
			m.cre_swap_scripts{wtasight=>gnsummm}
			m.item.alter{s_unstealable=1 s_undroppable=1}
		]
		
		array.new[comp_wpns]
		[[[
			bow05=>compb05
			sw1h01=>comps01
		]]]
		
		
		
		ACTION_PHP_EACH comp_wpns AS old=>new BEGIN
			ACTION_IF !FILE_EXISTS_IN_GAME "%new%.itm" BEGIN
				itm.copy[%old%=>%new%]
				[
					m.ab.alter{s_breakable=0}
					m.ab_fx.delete{s_opcode=309 OR s_opcode=146}
					m_droppable=0
				]
			END
		END

		cre.copy[orc01=>dw#ms1ow orc02=>dw#ms1oa|source_location=resource]
		[
			m_gender:=summoned
			m_allegiance:=controlled
			m_no_corpse=1
			m_xp_value=0
			m_level1=1
			m_hp=8
			m_thac0=20
			m_dv:=ms1orc
			m.cre_strip_scripts{gensht01 oarch02 orcwar}
			m.cre_swap_scripts{wtasight=>gnsummm wtarsgt=>gnsummm}
			m.cre_remove_items{rndtre01}
			m.cre_enforce_saves{}
			m.item.alter{s_unstealable=1 s_undroppable=1}
			IF {"%SOURCE_RES%" STR_EQ "orc01"}
			{{
				m_both_names:=@11
				m_animation:=ORC_MELEE
			}}
			ELSE
			{{
				m_both_names:=@12
				m.cre_swap_items{arow01=>arow02}
				m_animation:=ORC_RANGE
			}}
		]

		// main summoning

		LAF spl_make_summoning_2da STR_VAR monsters="DW#MS1OW DW#MS1OA DW#MS1HW DW#MS1HA MS1GOBA MS1GOBM" filename=msummo1 END
	END

END
