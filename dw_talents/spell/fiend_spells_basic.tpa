/////////////////////////////////////////////////////////////////////////////////////////////////////
///     Spells used by fiends; broken off because many are used by Smarter Mages and by Talents of Faerun
///
/////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION fiend_spells_basic BEGIN

	

   LAF check_label STR_VAR label="dw#fiend_spells_made" RET value END
   ACTION_IF !value BEGIN
   
	// on IWD, install the Tanarri spells if they're not present (this is a bug in IWDEE)
	
		ACTION_IF GAME_IS IWDEE && !VARIABLE_IS_SET TANARI_PARALYZE BEGIN
			OUTER_SET fail=0
			ACTION_FOR_EACH spl IN spin996 spin997 spin997a spin997b spin998 spin999 BEGIN
				ACTION_IF FILE_EXISTS "%MOD_FOLDER%/spell/resource/iwdfiend/%spl%.spl" BEGIN
					COPY "%MOD_FOLDER%/spell/resource/iwdfiend/%spl%.spl" override
				END ELSE BEGIN
					OUTER_SET fail=1
				END
			END
			ACTION_IF fail BEGIN
				WARN "fiend_spells_basic: cannot find IWD copies of the fiend spells"
			END
			APPEND "spell.ids" "3996 TANARI_DEATH_GAZE%WNL%3997 TANARI_VAMPIRIC_TOUCH%WNL%3998 TANARI_SILENCE%WNL%3999 TANARI_PARALYZE"
			OUTER_SPRINT TANARI_DEATH_GAZE SPIN996
			OUTER_SPRINT TANARI_VAMPIRIC_TOUCH SPIN997
			OUTER_SPRINT TANARI_SILENCE SPIN998
			OUTER_SPRINT TANARI_PARALYZE SPIN999
			COPY_EXISTING "%TANARI_DEATH_GAZE%.spl" override
				WRITE_LONG 0x8 23086
			IF_EXISTS
			COPY_EXISTING "%TANARI_VAMPIRIC_TOUCH%.spl" override
				WRITE_LONG 0x8 12071
			IF_EXISTS
			COPY_EXISTING "%TANARI_SILENCE%.spl" override
				WRITE_LONG 0x8 9187
			IF_EXISTS
			COPY_EXISTING "%TANARI_PARALYZE%.spl" override
				WRITE_LONG 0x8 2659
			IF_EXISTS
		END

       LAF make_label STR_VAR label="dw#fiend_spells_made" END

       LAF spl_make_innate_repeating_spell INT_VAR overwrite=1 allow_missing=1 STR_VAR arguments="%TANARI_PARALYZE% %TANARI_VAMPIRIC_TOUCH% %TANARI_DEATH_GAZE%" END

		// install the Deathknight fireball spell if absent (it is on IWDEE)
		ACTION_IF !FILE_EXISTS_IN_GAME "spin701.spl" BEGIN
			ACTION_IF FILE_EXISTS "%MOD_FOLDER%/spell/resource/iwdfiend/spin701.spl" BEGIN
				COPY "%MOD_FOLDER%/spell/resource/iwdfiend/spin701.spl" override
				APPEND "spell.ids" "3701%TAB%DEATHKNIGHT_FIREBALL" UNLESS "DEATHKNIGHT_FIREBALL"
				OUTER_SPRINT DEATHKNIGHT_FIREBALL "SPIN701"
			END ELSE BEGIN
				WARN "fiend_spells_basic: failed to find Deathknight Fireball (should be SPIN701.SPL in %MOD_FOLDER%/spell/resource/iwdfiend)"
			END
		END

	   ACTION_CLEAR_ARRAY patch_data
	   ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data BEGIN
          DEATHKNIGHT_FIREBALL=>INNATE_FIEND_FIREBALL
          WIZARD_IMPROVED_INVISIBILITY=>INNATE_FIEND_IMPROVED_INVISIBILITY
          WIZARD_TRUE_SIGHT=>INNATE_FIEND_TRUE_SIGHT
          WIZARD_DOMINATION=>INNATE_FIEND_DOMINATION
		  WIZARD_RAY_OF_ENFEEBLEMENT=>INNATE_FIEND_RAY_OF_ENFEEBLEMENT
       END
       ACTION_PHP_EACH patch_data AS key=>value BEGIN
			OUTER_SPRINT old_spell EVAL "%%key%%"
            LAF spl_make_innate_repeating_spell INT_VAR allow_missing=1 STR_VAR arguments="%old_spell%=>%value%" END
       END

	   COPY_EXISTING "%TANARI_DEATH_GAZE%.spl" override
			WRITE_ASCII 0x3a spwi605c
			LPF ALTER_SPELL_HEADER STR_VAR icon=spwi605b END
	   IF_EXISTS

       ACTION_IF !GAME_IS "IWDEE" BEGIN
			COPY_EXISTING "%SUCCUBUS_TELEPORT%.spl" "override/dw#fitel.spl" // fiendish teleport
				LPF ALTER_EFFECT INT_VAR match_opcode=124 timing=1 duration=0 END
	   END ELSE BEGIN
			ACTION_IF FILE_EXISTS  "%MOD_FOLDER%/spell/resource/iwdfiend/SPWI931.spl" BEGIN
					COPY "%MOD_FOLDER%/spell/resource/iwdfiend/SPWI931.spl" override
						WRITE_LONG 0x8 23357
						LPF ALTER_EFFECT INT_VAR match_opcode=124 timing=1 duration=0 END
						APPEND "spell.ids" "2931%TAB%SUCCUBUS_TELEPORT" UNLESS SUCCUBUS_TELEPORT
				END ELSE BEGIN
					WARN "fiend_spells_basic: failed to find template for dw#fitel.spl (should be SPWI931.SPL in %MOD_FOLDER%/spell/resource/iwdfiend)"
				END
	   END
   END
END

