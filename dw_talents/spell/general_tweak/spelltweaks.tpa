DEFINE_ACTION_FUNCTION spelltweaks BEGIN

	ACTION_FOR_EACH tweak IN 
		iron_body
		larloch
		energy_drain
		telekinesis
		boost_lower_resistance
		symbol_pain_no_save
		extra_symbols
		symbols_are_enchantment
		power_words_are_enchantment
	BEGIN
		LAF check_ini STR_VAR ini="%tweak%" RET value END
		ACTION_IF value BEGIN
			LAF run STR_VAR file="%tweak%" END
		END
	END






END

DEFINE_ACTION_FUNCTION swap_schools_for_these
	INT_VAR old_school=0
			new_school=0
			old_desc=0
			new_desc=0
	STR_VAR substring=""
BEGIN
	CLEAR_IDS_MAP
	COPY_EXISTING "spell.ids" "%workspace%"
		REPLACE_EVALUATE "\([A-Z0-9_]+%substring%[A-Z0-9_]+\)" BEGIN
			SPRINT ids "%MATCH1%"
			SPRINT resref EVAL "%%ids%%"
			PATCH_IF FILE_EXISTS_IN_GAME "%resref%.spl" BEGIN
				SPRINT scroll EVAL "%%ids%_SCROLL%"
				INNER_ACTION BEGIN
					LAF swap_school INT_VAR old_school new_school old_desc new_desc STR_VAR resref scroll END
				END					
			END
		END
		""
	BUT_ONLY	
END

DEFINE_ACTION_FUNCTION swap_school 
	INT_VAR old_school=0
			new_school=0
			old_desc=0
			new_desc=0
	STR_VAR resref=""
			scroll=""
BEGIN
	COPY_EXISTING "%resref%.spl" override
		PATCH_IF (BYTE_AT 0x25)=old_school BEGIN
			WRITE_BYTE 0x25 new_school	
			LPF SPL_enforce_school END
			READ_LONG 0x50 strref
			PATCH_IF strref>=0 BEGIN
				INNER_ACTION BEGIN
					ACTION_GET_STRREF strref desc
					OUTER_PATCH_SAVE desc "%desc%" BEGIN
						SPRINT old (AT old_desc)
						SPRINT new (AT new_desc)
						REPLACE_TEXTUALLY "%old%" "%new%"
					END
					STRING_SET_EVALUATE strref "%desc%"
				END
			END
			PATCH_FOR_EACH spec IN abjurer transmuter conjurer diviner enchanter invoker illusionist necromancer BEGIN
				LPF "SPL_read_unusable_%spec%" RET value END
				SET $status("%spec%")=value
			END
			INNER_ACTION BEGIN
				ACTION_IF FILE_EXISTS_IN_GAME "%scroll%.itm" BEGIN
					COPY_EXISTING "%scroll%.itm" override
						PATCH_FOR_EACH spec IN abjurer transmuter conjurer diviner enchanter invoker illusionist necromancer BEGIN
							SET arguments=$status("%spec%")
							LPF "ITM_unusable_%spec%"STR_VAR arguments END
						END		
				BUT_ONLY
				END			
			END
		END
	BUT_ONLY

	
END



