DEFINE_ACTION_FUNCTION revise_polymorph BEGIN

	LAF include STR_VAR file=lib_shapeshift.tph locbase=lib END
	LAF initialise_shapeshift END
	LAF polymorph_build_forms END
	ACTION_DEFINE_ASSOCIATIVE_ARRAY poly_arr BEGIN
		1=>"%spl_polymorph_hill_giant% %spl_polymorph_ogre_mage% %WIZARD_POLYMORPH_SPIDER% %WIZARD_POLYMORPH_MUSTARD_JELLY% %spl_polymorph_hell_hound% %spl_polymorph_rat%"
	END

	LAF make_shapeshift_wrap_spell INT_VAR description=50076 duration_base=60 duration_mult=18 grant_persistent_powers=1 STR_VAR spell="%WIZARD_POLYMORPH_SELF%" copy_spell="%WIZARD_POLYMORPH_SELF%" shapeshift_list=poly_arr END

	LAF build_shapeshift_ability_remover END




END


DEFINE_ACTION_FUNCTION polymorph_build_forms BEGIN

	///////////////
	// Hill giant
	///////////////
	
	
	ACTION_BASH_FOR "%MOD_FOLDER%/%component_loc%/resource/E293" ".*" BEGIN
		ACTION_IF !FILE_EXISTS_IN_GAME "%BASH_FOR_FILE%" BEGIN
			COPY "%BASH_FOR_FILESPEC%" override
		END
	END
	
	LAF make_shapeshift_spell
		INT_VAR str=19
				dex=10
				ac=3
				apr=1
				enchantment=1
				dicenumber=2
				dicesize=8
				name=50061
				description=50062
				timing=1
				casting_time=0
				auto_renew=1
				anim_code=0xe293 // 'GIANT_HILL' isn't always in animate.ids
		STR_VAR weapon="%itm_polymorph_hill_giant%"
				copy_weapon=sper01
				damagetype=piercing
				grant_spells="%spl_shapeshift_hill_giant_rage%"
				spell="%spl_polymorph_hill_giant%"
				copy_spell="%WIZARD_POLYMORPH_OGRE%"
	END
	
	spl.copy[%BARBARIAN_RAGE%=>%spl_shapeshift_hill_giant_rage%][]
	
	///////////////
	/// Ogre Mage
	///////////////
	
	LAF make_shapeshift_spell
		INT_VAR str=18
				str_ex=100
				dex=12
				ac=4
				apr=1
				enchantment=1
				dicenumber=1
				dicesize=10
				name=50063
				description=50064
				block_spellcasting=0
				timing=1
				casting_time=0
				auto_renew=1
				wipe_weapon_effects=1
		STR_VAR weapon="%itm_polymorph_ogre_mage%"
				copy_weapon=sw1h04
				damagetype=slashing
				spell="%spl_polymorph_ogre_mage%"
				copy_spell="%WIZARD_POLYMORPH_OGRE%"
				anim=OGRE_MAGE
				icon=dw-ssom
				ability_icon=dw-ssom
				icon_path="%MOD_FOLDER%/%component_loc%/resource"
	END

	
	/*
	///////////////
	/// Troll (at present only used for Cloak of the Sewers)
	///////////////
	
	LAF make_shapeshift_spell
		INT_VAR str=18
				str_ex=0
				dex=11
				ac=4
				apr=3
				enchantment=1
				name=50063
				description=50064
				block_spellcasting=0
				timing=1
				casting_time=0
				auto_renew=1
				wipe_weapon_effects=1
		STR_VAR weapon="plytroll"
				copy_weapon=sw1h04
				damagetype=slashing
				spell="%spl_polymorph_troll%"
				copy_spell="%WIZARD_POLYMORPH_OGRE%"
				anim=TROLL
				icon=spin154
				ability_icon=spin154
	END
	
	// shapeshift gives increased movement, regeneration 2 hp/second and immunity to paralysis, sleep, stun
	
	MAKE_PATCH
		add_effect_global_inline=>"opcode=>98 target=>1 timing=>2 parameter1=>2 parameter2=>2" // regeneration
		add_effect_global_inline'=>"opcode=>142 target=>1 timing=>2 parameter2=>87" // icon
		add_effect_global_inline''=>"opcode=>126 target=>1 timing=>2 parameter1=>11 parameter2=>1"  //movement
		add_effect_immunity=>sleep
		add_effect_immunity'=>hold
		add_effect_immunity''=>stun
	END
	
	*/
	
	//////////////////
	/// Mustard Jelly
	//////////////////
	
	//  basically unchanged 
	
	LAF make_shapeshift_spell
		INT_VAR use_template=1
				timing=1
				casting_time=0
				auto_renew=1
		STR_VAR spell="%WIZARD_POLYMORPH_MUSTARD_JELLY%"
	END

	//////////////////
	/// Giant Spider
	//////////////////
	
	// unchanged except for web spit
	
	LAF make_shapeshift_spell
		INT_VAR use_template=1
				timing=1
				description=50065
				casting_time=0
				auto_renew=1
		STR_VAR spell="%WIZARD_POLYMORPH_SPIDER%"
				grant_spells="%spl_shapeshift_spider_web%"
	END
	
	spl.copy[SPIN683=>"%spl_shapeshift_spider_web%"|source_location=resource]
	[
		m_ignore_dead_magic=1
		m_name:=@50066
		m_description:=@50067
		m.ab.alter{s_casting_time=0}
		m.ab_fx.alter{s_savebonus=0 s_save_vs_spell=0 s_save_vs_breath=1 s_dispel_resist=0|match="~%s_target%~==PresetTarget"}
	]

	/////////////////
	//// Hellhound
	/////////////////

	LAF make_shapeshift_spell
		INT_VAR 	str=18
					dex=15
					resistfire=100
					resistcold="-50"
					ac=4
					apr=1
					dicenumber=1
					dicesize=10
					name=50068
					description=50069
					timing=1
					casting_time=0
					auto_renew=1
		STR_VAR		damagetype=piercing
					spell="%spl_polymorph_hell_hound%"
					copy_spell="%WIZARD_POLYMORPH_WOLF%"
					weapon="%itm_polymorph_hell_hound%"
					copy_weapon=wolfm
					grant_spells="%spl_shapeshift_hell_hound_breath%"
					anim=wolf_vampiric
	END
	
	// fiery bite
	
	
	itm.edit[%itm_polymorph_hell_hound%]
	[
		m.ab_fx.add{s_opcode=12 s_target=2 s_timing=1 s_dicenumber=1 s_dicesize=6 s_parameter2b=8}
	]
	
	// breath weapon
	
	spl.copy[%WIZARD_BURNING_HANDS%=>%spl_shapeshift_hell_hound_breath%]
	[
		m_name:=@50070
		m_description:=@50071
		m.ab.delete{s_level>1}
		m.ab_fx.alter{s_resource:="%spl_shapeshift_hound_breath%"|match="s_opcode=324"}
		m.ab_fx.alter{s_parameter1=0 s_dicenumber=3 s_dicesize=6 s_save_vs_spell=0 s_save_vs_breath=1|match="s_opcode=12"}
	]
	LAF spl_make_innate_repeating_spell INT_VAR overwrite=1 delete_casting=1 cooldown=18 patch_description=0 new_is_ids=0 allow_missing=1 STR_VAR arguments="%spl_shapeshift_hell_hound_breath%" END 
	
	/////////////////
	//// Rat
	/////////////////
	
	LAF make_shapeshift_spell
		INT_VAR	str=10
				dex=20
				ac=4
				apr=1
				name=50072
				description=50073
				dicenumber=1
				dicesize=2
				timing=1
				casting_time=0
				auto_renew=1
		STR_VAR	damagetype=piercing
				spell="%spl_polymorph_rat%"
				copy_spell="%WIZARD_POLYMORPH_WOLF%"
				weapon="%itm_polymorph_rat%"
				copy_weapon=wolfm
				grant_spells="%spl_shapeshift_rat_hide%"
				anim=rat
				icon=polyrat
				ability_icon=polyrat
				icon_path="%MOD_FOLDER%/%component_loc%/resource"
	END
	
	// hide ability
	
	OUTER_SET hide_strref = RESOLVE_STR_REF( @50077 )
	
	spl.copy[%WIZARD_INVISIBILITY%=>%spl_shapeshift_rat_hide%]
	[
		m_name:=@50074
		m_description:=@50075
		m_non_combat=0
		m.ab.delete{s_level>1}
		m.ab.alter{s_target=5}
		m.ab_fx.delete{s_opcode=174 OR s_opcode=215}
		m.ab_fx.alter{s_parameter1=hide_strref|match="s_opcode=139"}
	]
	LAF spl_make_innate_repeating_spell INT_VAR patch_description=0 delete_casting=1 overwrite=1 new_is_ids=0 allow_missing=1 STR_VAR arguments="%spl_shapeshift_rat_hide%" END 

	APPEND_OUTER "%data_loc_shared%/shapeshift_applied_powers.txt" "%spl_shapeshift_rat_hide%"
	
	
END