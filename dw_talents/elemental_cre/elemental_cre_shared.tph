/////////////////////////////////////
/// These resources are also used by
/// druid shapeshift
/////////////////////////////////////

DEFINE_ACTION_FUNCTION make_shared_elemental_resources BEGIN
  OUTER_SPRINT component_loc elemental_cre
	
  LAF check_label STR_VAR label=dw#elemental_cre_effects RET value END
  ACTION_IF !value BEGIN
   LAF make_label STR_VAR label=dw#elemental_cre_effects END
   WITH_TRA "%scs_tra_loc%/english/elemental_cre.tra" "%scs_tra_loc%/%LANGUAGE%/elemental_cre.tra" BEGIN

// get aura projectile (copy of IWDEE SALAURA)

	ACTION_IF FILE_EXISTS_IN_GAME "dw-aura.pro" BEGIN
		OUTER_SET dw-aura = IDS_OF_SYMBOL (projectl dw-aura)	
	END ELSE BEGIN
	  ADD_PROJECTILE "%MOD_FOLDER%/%component_loc%/resource/dw-aura.pro"	
	  COPY_EXISTING "dw-aura.pro" override
		LPF check_ini STR_VAR ini=auras_are_party_friendly RET value END
		PATCH_IF value BEGIN
			WRITE_BYTE 0x200 BIT6 // party-friendly - too annoying otherwise
		END
	END


////////////////////////
////////////////////////
///  Earth
////////////////////////
////////////////////////

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Knockback effect
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	spl.make[%spl_earth_elemental_knockback_lesser%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=145 s_resource:="%sfo_filename%"} // elementals
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=132 s_resource:="%sfo_filename%"} // shadows 
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=133 s_resource:="%sfo_filename%"} // spectres 
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=134 s_resource:="%sfo_filename%"} // wraiths 
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=136 s_resource:="%sfo_filename%"} // mists 
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=146 s_resource:="%sfo_filename%"} // dragons
		m.ab_fx.add{s_opcode=235 s_target=2 s_timing=0 s_duration=1 s_parameter1=30 s_parameter2=2}
		m.ab_fx.add{s_opcode=39 s_target=2 s_timing=0 s_duration=2 s_parameter2=1}
		m.ab_fx.add{s_opcode=174 s_target=1 s_timing=4 s_duration=1 s_resource:=earth08}
		m.ab_fx.alter{s_save_vs_polymorph=1 s_savebonus=2}
	]

	spl.copy[%spl_earth_elemental_knockback_lesser%=>%spl_earth_elemental_knockback%]
	[
		m.ab_fx.alter{ s_savebonus=0| match="s_save_vs_polymorph=1"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]

	spl.copy[%spl_earth_elemental_knockback_lesser%=>%spl_earth_elemental_knockback_greater%]
	[
		m.ab_fx.alter{ s_savebonus="-2"| match="s_save_vs_polymorph=1"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Earth elemental weapons
// L8: 3d8  +knockback
// L12: 4d8+knockback
// L16+: 5d8+knockback
// prince: 5d10+knockback (the 5d10 is actually vanilla, so we only need to change the enchantment and add the knockback)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	LAF install INT_VAR overwrite=0 STR_VAR file=eelem.itm location=resource END

	itm.copy[eelem=>%itm_earth_elemental_weapon_8hd%]
	[
		m.ab.alter{s_dicenumber=3 s_dicesize=8 s_range=3}
		m_enchantment=3
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_earth_elemental_knockback_lesser%"}
	]

	itm.copy[eelem=>%itm_earth_elemental_weapon_12hd%]
	[
		m.ab.alter{s_dicenumber=4 s_dicesize=8 s_range=3}
		m_enchantment=4
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_earth_elemental_knockback%"}
	]

	itm.copy[eelem=>%itm_earth_elemental_weapon_16hd%]
	[
		m.ab.alter{s_dicenumber=5 s_dicesize=8 s_range=3}
		m_enchantment=4
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_earth_elemental_knockback_greater%"}
	]
	
	
	itm.edit[elemogre|allow_missing:i=1]
	[
		m_enchantment=5
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_earth_elemental_knockback_greater%"}		
	]

////////////////////////
////////////////////////
///  Fire
////////////////////////
////////////////////////

        OUTER_SET stat=IDS_OF_SYMBOL (stats "RESISTFIRE")
        LAF resolve_splprot_entry INT_VAR stat value=100 STR_VAR relation=less_than RET entry1=value END
        LAF resolve_splprot_entry INT_VAR stat value=128 STR_VAR relation=greater_equal RET entry2=value END
        LAF resolve_splprot_entry INT_VAR stat=0x104 value=entry1 relation_number=entry2 RET fire_immunity=value END


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Fire elemental weapons
// L8: 2d8 crushing + 2d6 fire + burn
// L12: 3d8 +3d6 fire + burn
// L16+: 4d8 +4d6 fire + burn
// Prince: 5d8 + 6d6 fire + burn
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	LAF install INT_VAR overwrite=0 STR_VAR file=fireelem.itm location=resource END

	itm.copy[fireelem=>%itm_fire_elemental_weapon_8hd%]
	[
		m.ab.alter{s_dicenumber=2 s_dicesize=8 s_range=3}
		m_enchantment=3
		m.ab_fx.alter{ s_dicenumber=2 s_dicesize=6| match="s_opcode=12"}
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_fire_elemental_burn_lesser%" s_save_vs_breath=1 s_savebonus=2}
	]

	itm.copy[%itm_fire_elemental_weapon_8hd%=>%itm_fire_elemental_weapon_12hd%]
	[
		m.ab.alter{s_dicenumber=3}
		m_enchantment=4
		m.ab_fx.alter{ s_dicenumber=3| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%spl_fire_elemental_burn%" s_savebonus=0| match="s_opcode=326"}
	]

	itm.copy[%itm_fire_elemental_weapon_8hd%=>%itm_fire_elemental_weapon_16hd%]
	[
		m.ab.alter{s_dicenumber=4}
		m_enchantment=4
		m.ab_fx.alter{ s_dicenumber=4| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%spl_fire_elemental_burn_greater%" s_savebonus="-2"| match="s_opcode=326"}
	]
	
	IF [FILE_EXISTS_IN_GAME "elemimix.itm"]
	[[
		itm.edit[elemimix]
		[
			m.ab.alter{s_dicenumber=5 s_dicesize=8 s_range=3}
			m_enchantment=5
			m.ab_fx.add{s_opcode=326 s_target=2 s_timing=1 s_resource:="%spl_fire_elemental_burn_greater%" s_save_vs_breath=1 s_savebonus="-2"}
		]

		//jmerry fixes (may be obviated by EEFP)
		spl.edit[spimixd|allow_missing:i=1]
		[
			m.ab_fx.alter{s_power=0}
			m.ab_fx.clone{s_opcode=206 s_parameter1=0 s_timing=10 s_duration=2 s_resource:=spimixd}
		
		]

	]]

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Burning aura - fire damage, 2d6 for lesser, 3d6 for medium, 4d6 for greater or prince. Save vs breath for 1/2; immune if 100% resistant to fire
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	spl.make[%spl_fire_elemental_aura_payload_lesser%]
	[
		m.ab.add{}
		m.ab.alter{s_projectile:=dw-aura}
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=fire_immunity s_resource:="%sfo_filename%"}
		m.ab_fx.add{s_opcode=12 s_target=2 s_timing=1 s_parameter2b=8 s_dicenumber=2 s_dicesize=6 s_save_vs_breath=1 s_save_for_half=1 s_savebonus=2}
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=6 s_resource:="%sfo_filename%"} // block for 1 rd
	]
	spl.copy[%spl_fire_elemental_aura_payload_lesser%=>%spl_fire_elemental_aura_payload%]
	[
		m.ab_fx.alter{ s_dicenumber=3 s_savebonus=0| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]
	spl.copy[%spl_fire_elemental_aura_payload_lesser%=>%spl_fire_elemental_aura_payload_greater%]
	[
		m.ab_fx.alter{ s_dicenumber=4 s_savebonus="-2"| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]

	// auras
	LAF spl_make_aura STR_VAR spell="%spl_fire_elemental_aura_lesser%" payload="%spl_fire_elemental_aura_payload_lesser%" END
	LAF spl_make_aura STR_VAR spell="%spl_fire_elemental_aura%" payload="%spl_fire_elemental_aura_payload%" END
	LAF spl_make_aura STR_VAR spell="%spl_fire_elemental_aura_greater%" payload="%spl_fire_elemental_aura_payload_greater%" END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Burning effects
// Display the burning effect; after delay of 3 secs, inflict damage - 1d6 lesser, 2d6 medium, 3d6 greater - and after delay of 6 secs, save vs breath to avoid iteration
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	OUTER_SET burning_string=RESOLVE_STR_REF (@4)

	spl.make[%spl_fire_elemental_burn_lesser%]
	[
		m.ab.add{}
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=fire_immunity s_resource:="%sfo_filename%"}
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=145 s_resource:="%sfo_filename%"} // elementals don't burn
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=132 s_resource:="%sfo_filename%"} // shadows don't burn
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=133 s_resource:="%sfo_filename%"} // spectres don't burn
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=134 s_resource:="%sfo_filename%"} // wraiths don't burn
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=136 s_resource:="%sfo_filename%"} // mists don't burn
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_parameter2=104 s_parameter1=144 s_resource:="%sfo_filename%"} // golems (mostly) don't burn
		m.ab_fx.add{s_opcode=139 s_target=2 s_parameter1=burning_string s_timing=1}
		m.ab_fx.add{s_opcode=215 s_target=2 s_parameter2=1 s_duration=7 s_resource:=soflamc}
	//	m.ab_fx.add{s_opcode=142 s_target=2 s_parameter2=94 s_duration=7}
		m.ab_fx.add{s_opcode=12 s_target=2 s_parameter2b=8 s_dicenumber=1 s_dicesize=6 s_timing=1}
		m.ab_fx.add{s_opcode=326 s_target=2 s_timing=4 s_duration=6 s_resource:="%sfo_filename%" s_save_vs_breath=1 s_savebonus=2}
	]

	spl.copy[%spl_fire_elemental_burn_lesser%=>%spl_fire_elemental_burn%]	
	[
		m.ab_fx.alter{ s_dicenumber=2 s_savebonus=0| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318 OR s_opcode=326"}
	]

	spl.copy[%spl_fire_elemental_burn_lesser%=>%spl_fire_elemental_burn_greater%]
	[
		m.ab_fx.alter{ s_dicenumber=3 s_savebonus="-2"| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318 OR s_opcode=326"}
	]

////////////////////////
////////////////////////
///  Air
////////////////////////
////////////////////////


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Air elemental whirlwind (aura payload)
// Zone of Sweet Air (target=self)
// air elementals immune - does slashing damage (2d8 for 8HD, 3d8 for 12HD, 4d8 for 16+HD - save vs breath for half) and gives 25% spell failure chance
// princes don't get this - superseded by their globe of blades
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	// payloads
	
	
	OUTER_SET whirlwind_string=RESOLVE_STR_REF (@3)
	spl.make[%spl_air_elemental_aura_payload_lesser%]
	[
		m.ab.add{}
		m.ab.alter{s_projectile:=dw-aura}
		m.ab_fx.add{s_opcode=273 s_target=1 s_timing=1} //zone of sweet air
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_resource:="%sfo_filename%" s_parameter1=186 s_parameter2=105} // immunity to following if AE
		m.ab_fx.add{s_opcode=139 s_target=2 s_timing=1 s_parameter1=whirlwind_string} // display string
		m.ab_fx.add{s_opcode=12 s_target=2 s_timing=1 s_parameter2b=256 s_dicenumber=2 s_dicesize=8 s_save_vs_breath=1 s_save_for_half=1 s_savebonus=2} // damage
		m.ab_fx.add{s_opcode=60 s_target=2 s_timing=0 s_duration=6 s_parameter1=25 s_parameter2=0} // casting failure mage
		m.ab_fx.add{s_opcode=60 s_target=2 s_timing=0 s_duration=6 s_parameter1=25 s_parameter2=1} // casting failure priest
		m.ab_fx.add{s_opcode=142 s_target=2 s_timing=0 s_duration=6 s_parameter2=83} // casting failure icon
		m.ab_fx.add{s_opcode=174 s_target=2 s_timing=1 s_resource:=eff_p88} // sound effect
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=6 s_resource:="%sfo_filename%"} // block for 1 rd
	]


	spl.copy[%spl_air_elemental_aura_payload_lesser%=>%spl_air_elemental_aura_payload%]
	[
		m.ab_fx.alter{ s_dicenumber=3 s_savebonus=0| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]

	spl.copy[%spl_air_elemental_aura_payload_lesser%=>%spl_air_elemental_aura_payload_greater%]
	[
		m.ab_fx.alter{ s_dicenumber=4 s_savebonus="-2"| match="s_opcode=12"}
		m.ab_fx.alter{ s_resource:="%sfo_filename%"| match="s_opcode=318"}
	]

	// auras
	LAF spl_make_aura STR_VAR spell="%spl_air_elemental_aura_lesser%" payload="%spl_air_elemental_aura_payload_lesser%" END
	LAF spl_make_aura STR_VAR spell="%spl_air_elemental_aura%" payload="%spl_air_elemental_aura_payload%" END
	LAF spl_make_aura STR_VAR spell="%spl_air_elemental_aura_greater%" payload="%spl_air_elemental_aura_payload_greater%" END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Extra damage to mist creatures
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	eff.make[%eff_air_elemental_extra_damage%]
	[
		m_opcode=12
		m_parameter2b=64
		m_dicenumber=2
		m_dicesize=8
	]

	
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Air elemental weapons
// L8: 2d10  + 1d6 electrical
// L12-16: 3d10 +2d6 electrical
// L20+: 4d10 +3d6 electrical
// Prince: 5d10 + 4d6 electrical
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	LAF install INT_VAR overwrite=0 STR_VAR file=aelem.itm location=resource END

	itm.copy[aelem=>%itm_air_elemental_weapon_8hd%]
	[
		m.ab.alter{s_dicenumber=2 s_dicesize=10 s_range=3}
		m_enchantment=3
		m.ab_fx.add{s_opcode=12 s_target=2 s_timing=1 s_dicenumber=1 s_dicesize=6 s_parameter2b=4}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=172 s_resource:="%eff_air_elemental_extra_damage%"}
	]

	itm.copy[%itm_air_elemental_weapon_8hd%=>%itm_air_elemental_weapon_12hd%]
	[
		m.ab.alter{s_dicenumber=3}
		m_enchantment=4
		m.ab_fx.alter{ s_dicenumber=2| match="s_opcode=12"}
	]

	itm.copy[%itm_air_elemental_weapon_8hd%=>%itm_air_elemental_weapon_16hd%]
	[
		m.ab.alter{s_dicenumber=4 s_range=3}
		m_enchantment=4
		m.ab_fx.alter{ s_dicenumber=3| match="s_opcode=12"}
	]
	
	itm.edit[elemyanc|allow_missing:i=1]
	[
		m.ab.alter{s_dicenumber=5 s_dicesize=10 s_range=3}
		m.enchantment=5
		m.ab_fx.alter{ s_dicenumber=4 s_dicesize=6| match="s_opcode=12"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=172 s_resource:="%eff_air_elemental_extra_damage%"}
	
	]

////////////////////////
////////////////////////
///  Water
////////////////////////
////////////////////////

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Quench (aura payload)
// Extinguishes fire shield (red), fire effects of Burning Blood and Shroud of Flame
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	
	OUTER_SET drenched_string=RESOLVE_STR_REF (@2)

	spl.make[%spl_water_elemental_quench%]
	[
		m.ab.add{}
		m.ab.alter{s_projectile:=dw-aura}
		m.ab_fx.add{s_opcode=139 s_target=2 s_timing=1 s_parameter1=drenched_string}
		m.ab_fx.add{s_opcode=321 s_target=2 s_timing=0 s_resource:="%WIZARD_FIRE_SHIELD_RED%"}
		m.ab_fx.add{s_opcode=112 s_target=2 s_timing=0 s_resource:=FBLADE}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=IDS_OF_SYMBOL (class ELEMENTAL_FIRE) s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=IDS_OF_SYMBOL (class GENIE_EFREETI) s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=IDS_OF_SYMBOL (class GENIE_NOBLE_EFREETI) s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=IDS_OF_SYMBOL (class RED_DRAGON) s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{opcode=318 s_target=2 s_timing=0 s_duration=6 s_resource:="%spl_water_elemental_quench%"}
		PATCH_IF VARIABLE_IS_SET WIZARD_SHROUD_OF_FLAME BEGIN
			m.ab_fx.add{s_opcode=321 s_target=2 s_timing=0 s_resource:="%WIZARD_SHROUD_OF_FLAME%"}
		END
	]
	
	
	LAF spl_make_aura STR_VAR spell="%spl_water_elemental_aura%" payload="%spl_water_elemental_quench%" END


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Drowning
// slowed, -4 to hit, -4 AC, -4 saves, can't spellcast, and 1d3 damage per second - lasts 1 round. Save vs spell negates, but we'll put the saving throw on the 146
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	// install the VVC if not already present
	
	LAF install INT_VAR overwrite=0 STR_VAR file="ohbenvlp.bam ohbenvlp.vvc" location=resource END

	// make main spell
	OUTER_SET drowning_string =RESOLVE_STR_REF (@1)
	spl.make[%spl_water_elemental_drown%]
	[
		m.ab.add{}
		// protection for various creature types
		m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_resource:="%sfo_filename%" s_parameter1=4 s_parameter2=103} // no effect on undead
		PATCH_FOR_EACH param IN 119 121 136 139 141 144 145 156 157 158 15 BEGIN
			m.ab_fx.add{s_opcode=318 s_target=2 s_timing=0 s_duration=1 s_resource:="%sfo_filename%" s_parameter1=param s_parameter2=104} // no effect on dragons, demons, mists, golems, elementals, slimes
		END
		// main payload
		m.ab_fx.add{s_opcode=0 s_target=2 s_timing=0 s_duration=6 s_parameter1="-4"} // AC penalty
		m.ab_fx.add{s_opcode=325 s_target=2 s_timing=0 s_duration=6 s_parameter1="-4"} // save penalty
		m.ab_fx.add{s_opcode=54 s_target=2 s_timing=0 s_duration=6 s_parameter1="-4"} // THAC0 penalty
		m.ab_fx.add{s_opcode=40 s_target=2 s_timing=0 s_duration=6} // slow effect
		m.ab_fx.add{s_opcode=60 s_target=2 s_timing=0 s_duration=6 s_parameter1=100 s_parameter2=0} // casting failure mage
		m.ab_fx.add{s_opcode=60 s_target=2 s_timing=0 s_duration=6 s_parameter1=100 s_parameter2=1} // casting failure priest
		m.ab_fx.add{s_opcode=12 s_target=2 s_timing=4 s_parameter2b=32 s_duration=entry_index+1 s_dicenumber=1 s_dicesize=3 |number:i=6}
		// visual indicators
		m.ab_fx.add{s_opcode=139 s_target=2 s_timing=1 s_parameter1=drowning_string}
		m.ab_fx.add{s_opcode=142 s_target=2 s_timing=0 s_duration=6 s_parameter2=41} // slow icon
		m.ab_fx.add{s_opcode=142 s_target=2 s_timing=0 s_duration=6 s_parameter2=83} // casting failure icon
		m.ab_fx.add{s_opcode=215 s_target=2 s_parameter2=1 s_duration=2 s_resource:=ohbenvlp}
	]
	
	LAF eff_make_casting_effect STR_VAR spell="%spl_water_elemental_drown%" effect="%eff_water_elemental_drown%" END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Extra damage to fire creatures
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	eff.make[%eff_water_elemental_extra_damage%]
	[
		m_opcode=12
		m_parameter2b=64
		m_dicenumber=2
		m_dicesize=8
	]

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Water elemental weapons
// L8: 3d6 
// L12-16: 4d6
// L20+: 5d6
// prince: 6d6
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

	LAF install INT_VAR overwrite=0 STR_VAR file=welem.itm location=resource END

	itm.copy[welem=>%itm_water_elemental_weapon_8hd%]
	[
		m.ab.alter{s_dicenumber=3 s_dicesize=6 s_range=3}
		m.fx.add{s_opcode=267 s_target=1 s_timing=2 s_parameter1=drenched_string} // immunity to 'drenched' string
		m_enchantment=3
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=2 s_resource:="%eff_water_elemental_drown%" s_save_vs_spell=1 s_savebonus=2}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=2 s_resource:="%eff_water_elemental_quench%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=187 s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=195 s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=197 s_resource:="%eff_water_elemental_extra_damage%"}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=198 s_resource:="%eff_water_elemental_extra_damage%"}
	]

	itm.copy[%itm_water_elemental_weapon_8hd%=>%itm_water_elemental_weapon_12hd%]
	[
		m.ab.alter{s_dicenumber=4}
		m_enchantment=4
		m.ab_fx.alter{ s_savebonus=0| match="s_save_vs_spell=1"}
	]

	itm.copy[%itm_water_elemental_weapon_8hd%=>%itm_water_elemental_weapon_16hd%]
	[		
		m.ab.alter{s_dicenumber=5}
		m_enchantment=4
		m.ab_fx.alter{ s_savebonus="-2"| match="s_save_vs_spell=1"}
	]
	
	itm.edit[elemhydr|allow_missing:i=1]
	[
		m.ab.alter{s_dicenumber=6 s_dicesize=6 s_range=3}
		m_enchantment=4
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=2 s_resource:="%eff_water_elemental_drown%" s_save_vs_spell=1 s_savebonus="-2"|insert_point:i=0}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=2 s_resource:="%eff_water_elemental_quench%"|insert_point:i=0}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=187 s_resource:="%eff_water_elemental_extra_damage%"|insert_point:i=0}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=195 s_resource:="%eff_water_elemental_extra_damage%"|insert_point:i=0}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=197 s_resource:="%eff_water_elemental_extra_damage%"|insert_point:i=0}
		m.ab_fx.add{s_opcode=177 s_target=2 s_timing=1 s_parameter2=5 s_parameter1=198 s_resource:="%eff_water_elemental_extra_damage%"|insert_point:i=0}
	]

	




   END // end of WITH_TRA wrap
  END // end of check_label wrap


END