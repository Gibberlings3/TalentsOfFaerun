IF
	Detect([PC])
	Global("Display","LOCALS",0)
	Alignment(Myself,MASK_EVIL)
	!HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #100
		SetGlobal("Display","LOCALS",1)
		DisplayStringHead(Myself,72788)  // Who calls Ogremoch, Prince of Earth?!
		ApplySpell(Myself,WIZARD_STONE_SKIN)
END

IF
	Detect([PC])
	Global("Display","LOCALS",0)
	!Alignment(Myself,MASK_EVIL)
	!HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #100
		SetGlobal("Display","LOCALS",1)
		DisplayStringHead(Myself,72798)  // Who calls Sunnis, Prince of Earth?!
		ApplySpell(Myself,WIZARD_STONE_SKIN)
END

IF
	Detect([PC])
	Global("Display","LOCALS",0)
	HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #100
		SetGlobal("Display","LOCALS",1)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
END

IF	
	!GlobalTimerNotExpired("castspell","LOCALS")
	See(NearestEnemyOf(Myself))
	HaveSpellRES("%spl_innate_stone_skin%")
	!GlobalTimerNotExpired("stoneskin","LOCALS")
	!CheckStatGT(Myself,0,STONESKINS)
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SetGlobalTimer("stoneskin","LOCALS",12)
		SpellRES("%spl_innate_stone_skin%",Myself)
END

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	See(NearestEnemyOf(Myself))
	HaveSpellRES("spogre01") 
	Alignment(Myself,MASK_EVIL)
	!HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #200
		SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpellRES("spogre01",Myself)
		DisplayStringHead(Myself,72789)  // Feel the wrath of Ogremoch!
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	See(NearestEnemyOf(Myself))
	HaveSpellRES("spogre01")
	OR(2)
		ActionListEmpty()
		Allegiance(Myself,ENEMY)
	!Alignment(Myself,MASK_EVIL)
	!HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #200
		SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpellRES("spogre01",Myself)
		DisplayStringHead(Myself,72799)  // Feel the wrath of Sunnis!
	RESPONSE #100
		Continue()
END

IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	See(NearestEnemyOf(Myself))
	HaveSpellRES("spogre01")
	OR(2)
		ActionListEmpty()
		Allegiance(Myself,ENEMY)
	HasItem("%itm_elemental_prince_no_text%",Myself)
THEN
	RESPONSE #200
		SetGlobalTimer("castspell","LOCALS",6)
		ReallyForceSpellRES("spogre01",Myself)
	RESPONSE #100
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("castspell","LOCALS")
	OR(2)
		ActionListEmpty()
		Allegiance(Myself,ENEMY)
	HaveSpellRES("%spl_innate_flesh_to_stone%")
THEN
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SpellRES("%spl_innate_flesh_to_stone%",NearestEnemyOf(Myself))  // Flesh to Stone
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SpellRES("%spl_innate_flesh_to_stone%",SecondNearestEnemyOf(Myself))  // Flesh to Stone
	RESPONSE #100
		SetGlobalTimer("castspell","LOCALS",6)
		SpellRES("%spl_innate_flesh_to_stone%",ThirdNearestEnemyOf(Myself))  // Flesh to Stone
	RESPONSE #100
		Continue()
END
			
IF
	!GlobalTimerNotExpired("castspell","LOCALS")
	HaveSpellRES("%spl_earth_elemental_spike_stones%")
	See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100	
		SetGlobalTimer("castspell","LOCALS",6)
		ForceSpellRES("%spl_earth_elemental_spike_stones%",LastSeenBy(Myself))
	RESPONSE #200
		Continue()
END
				