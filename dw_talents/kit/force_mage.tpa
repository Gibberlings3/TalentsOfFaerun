DEFINE_ACTION_FUNCTION force_mage BEGIN

	

	// get ini entries
	
	LAF check_ini STR_VAR ini=number_of_required_force_mage_spells RET number_required=value END
	LAF check_ini STR_VAR ini=list_specialist_spells RET list_spells=value END

	// main spell system

	LAF include STR_VAR file=elementalist_shared.tph END
	LAF make_elemental_spell_lists RET all_names END
	LAF make_force_spell_lists END
	
	// virtualised specialists (needed to avoid risk of wizard kits erroneously picking up specialist restrictions)
	
	LAF include STR_VAR file=mageschools_shared.tph locbase=shared END
	LAF virtualise_specialist_blocks STR_VAR tra=mageschool END
	
	// generate the class description
	OUTER_SPRINT desc @2
	OUTER_PATCH_SAVE desc "%desc%" BEGIN
		REPLACE_TEXTUALLY BLOCKED_SPELL_PLACEHOLDER "%all_names%"
	END
	OUTER_SET strref=RESOLVE_STR_REF ("%desc%")

	// make the 'force focus' spell used in CLAB
	
	spl.make[%spl_force_mage_innate%]
	[
		m.ab.add{}
		m.ab_fx.add{s_timing=9 s_target=1 s_opcode=31 s_parameter1=10 s_parameter2=0} // resist magic damage
		m.ab_fx.add{s_timing=9 s_target=1 s_opcode=332 s_parameter1=5 s_parameter2=20} // extra magic damage
		m.ab_fx.add{s_timing=9 s_target=1 s_opcode=326 s_resource:=dwmcmb} // bonus spell for dualclass characters
	]
	
	// make the kit
	
	kit.copy[MAGE=>DW_FORCE_MAGE]
	[
		k_name:=@1	
		k_description:=@2
		k_description=k_description.patch{
			REPLACE_TEXTUALLY BLOCKED_SPELL_PLACEHOLDER "%all_names%"		
		}
		k.kit_apply_powers{%spl_force_mage_innate% 1}
		k_color_minor_cloth=28
		k_color_main_cloth=63
	]
	
	// assign spell lists
	
	LAF set_spell_list INT_VAR update_scrolls_later=1 STR_VAR kit=DW_FORCE_MAGE block_learn="mage_earth mage_air mage_fire mage_water" END	
	LAF set_specialist_spells INT_VAR number_required list_spells update_description=1 STR_VAR kit=DW_FORCE_MAGE spell_list="force" END

	// assign learning modifiers
	
	LAF set_spell_learn_modifiers STR_VAR kit=DW_FORCE_MAGE modifiers="force=25" END
	

	LAF bonus_spells_from_list STR_VAR kit=DW_FORCE_MAGE path="%MOD_FOLDER%/class_race/bonus_spell_lists/force.txt" END
	
	// if appropriate, handle multiclass
	
	LAF check_label STR_VAR label=dw#multiclass_specialists_permitted RET value END
	ACTION_IF value BEGIN
		LAF multiclass_force_mages END
	END
	
	// block illegal items
	
	LAF exclude_elemental_items STR_VAR kit=DW_FORCE_MAGE element=any END
	
	// update the kit-identification LUA
	LAF ui_detect_class_kit END

	// re-externalize proficiencies if appropriate
	ACTION_IF FILE_EXISTS "%data_loc_shared%/weapprof.2da" BEGIN
		LAF externalize_weapprof END
	END
	// process scroll usability
	LAF process_scroll_usability_now END

	// if appropriate, rebuild dual-class grid
	
	ACTION_IF FILE_EXISTS_IN_GAME "m_dw_dud.lua" BEGIN
		LAF collect_dual_class_kit_data END
	END
	
	
	LAF 3p_kit END

END



DEFINE_ACTION_FUNCTION multiclass_force_mages BEGIN

	
	OUTER_SPRINT class ""
	ACTION_FOR_EACH this_class IN fighter_mage cleric_mage mage_thief fighter_mage_thief fighter_mage_cleric BEGIN
		LAF check_ini STR_VAR ini="force_mage_%this_class%" RET value END
		ACTION_IF value BEGIN
			ACTION_TO_UPPER this_class
			OUTER_SPRINT class "%class%%this_class% "
		END
	END
	
	LAF 2daq_extract STR_VAR resref=kitlist column=ROWNAME RET_ARRAY kitid_array=array END
	LAF array_invert STR_VAR array=kitid_array RET_ARRAY kitid_array=array END
	LAF 2da_read STR_VAR file=kittable.2da RET_ARRAY kittable_array=array END
	ACTION_FOR_EACH race IN human elf half_elf dwarf halfling gnome halforc BEGIN
		ACTION_TO_UPPER race
		ACTION_FOR_EACH class IN FIGHTER_MAGE CLERIC_MAGE MAGE_THIEF FIGHTER_MAGE_THIEF FIGHTER_MAGE_CLERIC BEGIN
			OUTER_SPRINT table $kittable_array("%class%" "%race%")
			OUTER_SET kit_id=$kitid_array("DW_FORCE_MAGE")
			LAF kit_set_table INT_VAR present=1 kit_id STR_VAR table END
		END
	END
		

	// virtual names for chargen
		
  LAF override_chargen_kit_name INT_VAR override=21 STR_VAR kit=DW_FORCE_MAGE class=FIGHTER_MAGE END
  LAF override_chargen_kit_name INT_VAR override=22 STR_VAR kit=DW_FORCE_MAGE class=CLERIC_MAGE END
  LAF override_chargen_kit_name INT_VAR override=23 STR_VAR kit=DW_FORCE_MAGE class=MAGE_THIEF END
  LAF override_chargen_kit_name INT_VAR override=24 STR_VAR kit=DW_FORCE_MAGE class=FIGHTER_MAGE_THIEF END
  LAF override_chargen_kit_name INT_VAR override=25 STR_VAR kit=DW_FORCE_MAGE class=FIGHTER_MAGE_CLERIC END


	
	



END