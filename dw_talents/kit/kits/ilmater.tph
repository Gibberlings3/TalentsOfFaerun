DEFINE_ACTION_FUNCTION ilmater_cleric BEGIN


	OUTER_SPRINT kit DW_ILMATER_CLERIC
	LAF splconv_get_name STR_VAR spell="%CLERIC_REMOVE_FEAR%" new_type=innate RET  spl_ilmater_remove_fear=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_HEAL%" new_type=innate RET spl_ilmater_heal=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_FAVOR_OF_ILMATER%" new_type=innate RET spl_ilmater_favor=new_resref END
	LAF ilmater_shared END

	kit.copy[CLERIC=>%kit%]
	[
		k_name:=@44
		k_desc:=@45
		k.kit_apply_powers{%spl_ilmater_innate% 1}
		k.kit_grant_powers{%spl_ilmater_remove_fear% 1 4,%spl_ilmater_favor% 3 5,%spl_ilmater_endurance% 7,%spl_ilmater_heal% 9 6}
		k_color_metal=25
		k_color_minor_cloth=43
		k_color_main_cloth=30
		k_color_leather=2
		k_color_armor=24
		k.kit_set_alignment{lg ng ln}
		IF {is_iwd} {{k_bio:=@1010}}	
	]
	
	LAF cleric_kit_substitutions STR_VAR kit god=ilmater END
	LAF assign_spheres STR_VAR kit default=cleric subtract="combat necromantic evil" add=sun END

	LAF holy_symbol INT_VAR name=2010 follower=2011 STR_VAR kit desc_bam=cdpilhly inv_bam=cdiilhly bam_path=dr END
	LAF adjust_cleric_stronghold STR_VAR lathander=all kit END

END

DEFINE_ACTION_FUNCTION abjurer_of_ilmater BEGIN

	OUTER_SPRINT kit DW_ILMATER_ABJURER
	LAF splconv_get_name STR_VAR spell="%CLERIC_REMOVE_FEAR%" new_type=innate RET  spl_ilmater_remove_fear=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_HEAL%" new_type=innate RET spl_ilmater_heal=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_FAVOR_OF_ILMATER%" new_type=innate RET spl_ilmater_favor=new_resref END
	LAF ilmater_shared END

	kit.copy[CLERIC_MAGE=>%kit%]
	[
		k_name:=@49
		k_desc:=@50
		k.kit_apply_powers{%spl_ilmater_innate% 1|class=cleric}
		k.kit_grant_powers{%spl_ilmater_remove_fear% 1 4,%spl_ilmater_favor% 3 5,%spl_ilmater_endurance% 7,%spl_ilmater_heal% 9 6|class=cleric}
		k_color_minor_cloth=43
		k_color_main_cloth=30
		k.kit_set_alignment{lg ng ln}
		IF {is_iwd} {{k_bio:=@1010}}
	]
	
	LAF cleric_kit_substitutions STR_VAR kit god=ilmater END
	LAF assign_spheres INT_VAR update_scroll_usability=0 STR_VAR kit default=cleric subtract="combat necromantic evil" add=sun END
	LAF holy_symbol INT_VAR name=2010 follower=2011 STR_VAR kit desc_bam=cdpilhly inv_bam=cdiilhly bam_path=dr END
	LAF adjust_cleric_stronghold STR_VAR lathander=all kit END

	LAF make_multiclass_specialist STR_VAR kit school=abjuration END
	LAF set_multiclass_components STR_VAR kit components="DW_ILMATER_CLERIC ABJURER"  END






END

DEFINE_ACTION_FUNCTION ilmater_favored_soul BEGIN

	LAF ilmater_shared END
	LAF splconv_get_name STR_VAR spell="%CLERIC_REMOVE_FEAR%" new_type=innate RET  spl_ilmater_remove_fear=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_HEAL%" new_type=innate RET spl_ilmater_heal=new_resref END
	LAF splconv_get_name STR_VAR spell="%CLERIC_FAVOR_OF_ILMATER%" new_type=innate RET spl_ilmater_favor=new_resref END
	OUTER_SPRINT kit DW_ILMATER_FAVORED
	LAF build_favored_soul_kit INT_VAR name=4083 desc=4084 god=4086 god_gender=4005 god_temple=4085 STR_VAR kit copy_from=DW_ILMATER_CLERIC END
	LAF cleric_kit_substitutions STR_VAR kit god=ilmater END

	kit.edit[%kit%|edit_strrefs_in_place:i=1]
	[
		k.kit_apply_powers{%spl_ilmater_innate% 1}
		k.kit_grant_powers{%spl_ilmater_remove_fear% 1 4,%spl_ilmater_favor% 3 5,%spl_ilmater_endurance% 7,%spl_ilmater_heal% 9 6}
	]

	LAF assign_bonus_spells STR_VAR kit  spells="CLERIC_CURE_LIGHT_WOUNDS CLERIC_DRAW_UPON_HOLY_MIGHT CLERIC_EXALTATION CLERIC_NEUTRALIZE_POISON CLERIC_MASS_CURE CLERIC_HEAL CLERIC_REGENERATE" END

END	


DEFINE_ACTION_FUNCTION ilmater_shared BEGIN

	LAF include STR_VAR file=log_favored_weapon.tph locbase=shared END
	LAF log_favored_weapon STR_VAR god=ILMATER weapon=quarterstaff END


	LAF check_label STR_VAR label=dw#ilmater_shared RET value END
	ACTION_IF !value BEGIN
	LAF make_label STR_VAR label=dw#ilmater_shared END

		// Ilmater's endurance
		
		spl.copy[cdilend=>%spl_ilmater_endurance%|source_location=dr]
		[
			m_name:=@1035
			m_description:=@1036
		
		]
		
		LAF install STR_VAR files="cdilendb.bam cdilendc.bam" location=dr END

		// immunity to pain
		
		spl.make[%spl_ilmater_innate%]
		[
			m.ab.add{}
			m.ab_fx.add{s_opcode=318 s_target=2 s_timing=9 s_resource:="%CLERIC_SYMBOL_PAIN%"}
		]
		
		COPY_EXISTING "%spl_ilmater_innate%.spl" override
		  PATCH_FOR_EACH resource IN CLERIC_SYMBOL_PAIN INFLICT_PAIN WIZARD_SYMBOL_PAIN NPC_SYMBOL_PAIN BEGIN
			PATCH_IF VARIABLE_IS_SET "%resource%" BEGIN
				LPF CLONE_EFFECT STR_VAR match_resource="%CLERIC_SYMBOL_PAIN%" resource END
			END
		  END
		 BUT_ONLY

		// other spells
		
		LAF splconv STR_VAR spell="%CLERIC_REMOVE_FEAR%" new_type=innate new_resref="%spl_ilmater_remove_fear%" END
		LAF splconv STR_VAR spell="%CLERIC_HEAL%" new_type=innate new_resref="%spl_ilmater_heal%" END
		LAF splconv STR_VAR spell="%CLERIC_FAVOR_OF_ILMATER%" new_type=innate new_resref="%spl_ilmater_favor%" END
		



	END

END