DEFINE_ACTION_FUNCTION kit_fc BEGIN


	LAF include STR_VAR file=symbol_stronghold.tph locbase=shared END
	LAF include STR_VAR file=cleric_dialog_adjusts.tph END
	LAF include STR_VAR file=multiclass.tph locbase=shared END


	ACTION_FOR_EACH fc IN
		raging_flame_of_kossuth
		axe_of_clangeddin
		battlerager_of_clangeddin
		wrathful_spear_of_gruumsh
		hammer_of_moradin
		berserkerpriest_of_tempus
		fury_of_talos
		sword_of_selune
		templar_of_lathander
		magehunter_of_helm
		crusader_of_tyr
		guardian_of_helm
		knight_of_bane_xvim
	BEGIN
		LAF check_ini STR_VAR ini="%fc%" RET value END
		ACTION_IF is_iwd BEGIN
			LAF check_ini INT_VAR silent=1 STR_VAR ini="iwd_story_block_%fc%" RET story_block=value END
		END ELSE BEGIN
			LAF check_ini INT_VAR silent=1 STR_VAR ini="bg_story_block_%fc%" RET story_block=value END		
		END
		ACTION_IF value && !story_block BEGIN
			OUTER_PATCH "%fc%" BEGIN
				REPLACE_EVALUATE "of_\(.*\)" BEGIN
					SPRINT god "%MATCH1%"
				END
				""
			END
			LAF include STR_VAR file="%god%.tph" location=kits END
			ACTION_TIME "%fc%" BEGIN 
				LAF "%fc%" END
			END
		END
	END
	

	// update the kit-identification LUA
	LAF ui_detect_class_kit END

	
	// externalize proficiencies
	LAF externalize_weapprof END

	// if appropriate, rebuild dual-class grid
	
	ACTION_IF FILE_EXISTS_IN_GAME "m_dw_dud.lua" BEGIN
		LAF collect_dual_class_kit_data END
	END

	
	// NPCs
	
	LAF 3p_kit END

END
