//////////////////////////////////////////////////////////////////////////////////////
//// Give blades, jesters and skalds the ability to select their own song at level 1
//////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bardsong BEGIN

	// blades can just get the baseline ability
	kit.edit[BLADE]
	[
		k.kit_grant_powers{#BARD1 1}
	]

	// skalds need their own
	
	LAF make_bardsong_granter 
		INT_VAR name_tra_ref=66
		STR_VAR kit=SKALD
				new_bam=dw-sng1
				new_bam_path="%MOD_FOLDER%/%component_loc%/resource/BARD_SKALD.bam"
				new_spell="%spl_use_skald_bardsong%"
				granted_spell="spcl542a"
	END
	
	// so do jesters
	
	LAF make_bardsong_granter 
		INT_VAR name_tra_ref=67
		STR_VAR kit=JESTER
				new_bam=dw-sng2
				new_bam_path="%MOD_FOLDER%/%component_loc%/resource/BARD_JESTER.bam"
				new_spell="%spl_use_jester_bardsong%"
				granted_spell="spcl751a"
	END


END

DEFINE_ACTION_FUNCTION make_bardsong_granter
	INT_VAR name_tra_ref=0
	STR_VAR kit=""
			new_bam=""
			new_bam_path=""
			new_spell=""
			granted_spell=""
BEGIN
	COPY "%new_bam_path%" "override/%new_bam%.bam"
	COPY_EXISTING "#BARD1.spl" "override/%new_spell%.spl"
		SAY 0x8 (AT name_tra_ref)
		LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource="%new_spell%" END
		LPF ALTER_EFFECT INT_VAR match_opcode=171 STR_VAR resource="%new_spell%" END
		LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource="%granted_spell%" END
		REPLACE_TEXTUALLY "BARD1" "%new_bam%" (8)
		REPLACE_TEXTUALLY "BARD1B" "%new_bam%" (8)
	BUT_ONLY
	kit.edit[%kit%]
	[
		k.kit_grant_powers{%new_spell% 1}
	]



END