DEFINE_ACTION_FUNCTION make_feats
BEGIN

    // initialise
    
	
	OUTER_SPRINT require_string @40
	
     ACTION_IF !FILE_EXISTS "%data_loc_shared%/dw#hla_feat.2da" BEGIN
       COPY ".../stratagems-inline/blank" "%data_loc_shared%/dw#hla_feat.2da"
     END

     // load in the file names
     OUTER_SPRINT array_to_read feat_table.2da
     OUTER_SPRINT read_array_location "%MOD_FOLDER%/%component_loc%/data"
     OUTER_SPRINT array_name feat_files
     LAM read_in_assoc_array

     // do setup for various
     
     LAF run STR_VAR file="item_crafting_use" END

     LAF include STR_VAR file="shared_hla_feat_setup.tph" END
	 LAF empowered_healing_setup END


     // make and label the feats
     LAF setup_feats END
     // add the feats' actual abilities
     LAF make_feats_wrapper END
     
     // ranger powers
     LAF hated_enemies END

     // tidy up
     
     COPY "%data_loc_shared%/dw#hla_feat.2da" "%data_loc_shared%"
        PRETTY_PRINT_2DA


END


/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Build the skeletons of the feats - name, description, but no payload
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION setup_feats BEGIN

    LAF process_table STR_VAR table=feat_table.2da location=data function=setup_feats_core  END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION setup_feats_core
       STR_VAR spell="" name="-1" desc="-1" descstring="" prereq_string="-1"  namestring="" special=""  feat_id="" type=""
BEGIN
	PRINT "Trying to make %feat_id%"
    ACTION_IF FILE_EXISTS_IN_GAME "%spell%.spl" BEGIN
       ACTION_MATCH "%special%" WITH
          overwrite do_not_make only_if_absent BEGIN END
          DEFAULT
             LAF warning STR_VAR warning="feat file %spell% overwrites an already-existing file" END
       END
    END
    OUTER_SET proceed=1
    ACTION_IF "%special%" STRING_EQUAL_CASE "do_not_make" BEGIN 
       ACTION_IF FILE_EXISTS_IN_GAME "%spell%.spl" BEGIN
         ACTION_CLEAR_ARRAY patch_data
         ACTION_IF name>=0 BEGIN
            OUTER_SPRINT $patch_data("set_name") (AT name)
         END
         ACTION_IF desc>=0 BEGIN
            OUTER_SPRINT descstring (AT desc)
         END ELSE BEGIN
            COPY_EXISTING "%spell%.spl" override
                 READ_STRREF 0x50 descstring
            BUT_ONLY
         END
         ACTION_IF prereq_string>=0 BEGIN
          OUTER_SPRINT extra (AT prereq_string)
          OUTER_SPRINT descstring "%descstring%%WNL%%WNL%%require_string%%extra%"
         END
         OUTER_SPRINT $patch_data("set_description") "%descstring%"
         LAF edit_spell STR_VAR spell edits=patch_data END
       END
    END ELSE BEGIN
       ACTION_IF "%namestring%" STRING_EQUAL "" BEGIN
		ACTION_IF name>=0 BEGIN
            OUTER_SPRINT namestring (AT name)
		END ELSE BEGIN
			OUTER_SPRINT namestring PLACEHOLDER
		END
       END 
       ACTION_IF "%descstring%" STRING_EQUAL "" BEGIN
		ACTION_IF desc>=0 BEGIN
            OUTER_SPRINT descstring (AT desc)
		END ELSE BEGIN
			OUTER_SPRINT descstring PLACEHOLDER
		END
       END
       OUTER_SPRINT descstring "%namestring%%WNL%%descstring%"
       ACTION_IF prereq_string>=0 BEGIN
          OUTER_SPRINT extra (AT prereq_string)
          OUTER_SPRINT descstring "%descstring%%WNL%%WNL%%require_string%%extra%"
       END
       MAKE_PATCH
           add_basic_ability=>null
           ability_target=>5
           set_name=>"%namestring%"
           set_description=>"%descstring%"
       END
       LAF make_spell STR_VAR spell edits=patch_data END
    END
    // add to table
    APPEND_OUTER "%data_loc_shared%/dw#hla_feat.2da" "%feat_id% %spell% %type%"
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_feats_wrapper BEGIN

     OUTER_SPRINT default "timing=>9 target=>1"

     // dash
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>126 parameter1=>2"
     END
     LAF make_feats_core STR_VAR feat=DASH END

     // aegis of rime

     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>28 parameter1=>10 parameter2=>0" // resist cold
          add_effect_inline'=>"%default% opcode=>332 parameter1=>2 parameter2=>20" // extra cold damage
     END
     LAF make_feats_core STR_VAR feat=AEGIS_OF_RIME END

     // aqua mortis

     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>27 parameter1=>10 parameter2=>0" // resist acid
          add_effect_inline'=>"%default% opcode=>332 parameter1=>4 parameter2=>20" // extra acid damage
     END
     LAF make_feats_core STR_VAR feat=AQUA_MORTIS END

     // scion of storms
     
     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>29 parameter1=>10 parameter2=>0" // resist electricity
          add_effect_inline'=>"%default% opcode=>332 parameter1=>3 parameter2=>20" // extra electric damage
     END
     LAF make_feats_core STR_VAR feat=SCION_OF_STORMS END

     // spirit of flame
     
     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>30 parameter1=>10 parameter2=>0" // resist fire
          add_effect_inline'=>"%default% opcode=>332 parameter1=>1 parameter2=>20" // extra fire damage
     END
     LAF make_feats_core STR_VAR feat=SPIRIT_OF_FLAME END

     // dodge
     
     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>0 parameter1=>1"
     END
     LAF make_feats_core STR_VAR feat=DODGE END

     // improved initiative

     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>190 parameter1=>1" // weapon speed
          add_effect_inline'=>"%default% opcode=>189 parameter1=>1" // weapon speed
     END
     LAF make_feats_core STR_VAR feat=IMPROVED_INITIATIVE END
     
     // improved critical

     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>301 parameter1=>1 mode=>1"
     END
     LAF make_feats_core STR_VAR feat=IMPROVED_CRITICAL_MELEE END

     MAKE_PATCH
          add_effect_inline=>"%default% opcode=>301 parameter1=>1 mode=>1"
     END
     LAF make_feats_core STR_VAR feat=IMPROVED_CRITICAL_RANGE END

     // dirty fighting
     
     // make the payload spell
     
     OUTER_SET dirty_blind_string = RESOLVE_STR_REF (@153)
     OUTER_SET dirty_slow_string = RESOLVE_STR_REF (@154)
     MAKE_PATCH
        add_basic_ability=>null
        ability_target=>1
        add_effect_inline'7=>"opcode=>206 target=>2 duration=>30 resource=>DW#DIRTY parameter1=>~-1~"  // non-cumulative
        add_effect_inline=>"opcode=>74 target=>2 duration=>12 probability1=>50 save_vs_death=>1"
        add_effect_inline'0=>"opcode=>142 target=>2 duration=>12 parameter2=>8 probability1=>50 save_vs_death=>1"
        add_effect_inline'1=>"opcode=>139 target=>2 timing=>1 parameter1=>%dirty_blind_string% probability1=>50 save_vs_death=>1"
        add_effect_inline'2=>"opcode=>40 target=>2 duration=>30 probability1=>100 probability2=>51 save_vs_death=>1"
        add_effect_inline'3=>"opcode=>54 target=>2 duration=>30 probability1=>100 probability2=>51 parameter1=>~-4~ save_vs_death=>1"
        add_effect_inline'4=>"opcode=>142 target=>2 duration=>30 parameter2=>41 probability1=>100 probability2=>51 save_vs_death=>1"
        add_effect_inline'5=>"opcode=>139 target=>2 timing=>1 parameter1=>%dirty_slow_string% probability1=>100 probability2=>51 save_vs_death=>1"
        add_effect_inline'6=>"opcode=>324 target=>2 duration=>1 resource=>DW#DIRTY parameter2=>107 parameter1=>2"
        add_effect_inline'8=>"opcode=>324 target=>2 duration=>1 resource=>DW#DIRTY parameter2=>107 parameter1=>4"
        add_effect_inline'9=>"opcode=>324 target=>2 duration=>1 resource=>DW#DIRTY parameter2=>107 parameter1=>255"
     END
     LAF make_spell STR_VAR spell=DW#DIRTY edits=patch_data END

     // make the application spell
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>341 resource=>DW#DIRTY"
     END
     LAF make_feats_core STR_VAR feat=DIRTY_FIGHTING END
 /*
    // make the payload spell
     OUTER_SET crippling_string = RESOLVE_STR_REF (@34)
     LAF add_to_statdesc STR_VAR string=crippling_string bam=SPWI518C RET stat_num END
     MAKE_PATCH
        add_basic_ability=>null
        ability_target=>1
        add_effect_inline'0=>"opcode=>54 target=>2 parameter1=>~-2~ duration=>60" // thac0 penalty
        add_effect_inline'1=>"opcode=>73 target=>2 parameter1=>~-2~ duration=>60" // damage penalty
        add_effect_inline'2=>"opcode=>139 target=>2 duration=>60 parameter1=>%crippling_string%"
        add_effect_inline'3=>"opcode=>142 target=>2 duration=>60 parameter2=>%stat_num%"
        add_effect_inline=>"opcode=>321 target=>2 resource=>dw#cripp duration=>1" // non-cumulative
     END
     LAF make_spell STR_VAR spell=DW#CRIPP edits=patch_data END
     
     // make the application spell

     MAKE_PATCH
         add_effect_inline=>"%default% opcode=>340 resource=>dw#cripp"
     END
     LAF make_feats_core STR_VAR feat=CRIPPLING_STRIKE END
 */
     // mighty blows / deadly aim
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>285 parameter1=>1"
     END
     LAF make_feats_core STR_VAR feat=MIGHTY_BLOWS END

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>286 parameter1=>1"
     END
     LAF make_feats_core STR_VAR feat=DEADLY_AIM END

     // saving throw boosts
     
     MAKE_PATCH
         add_effect_inline=>"opcode=>33 parameter1=>2 %default%"
         add_effect_inline'=>"opcode=>35 parameter1=>2 %default%"
     END
     LAF make_feats_core STR_VAR feat=GREAT_FORTITUDE END

     MAKE_PATCH
         add_effect_inline=>"opcode=>34 parameter1=>2 %default%"
         add_effect_inline'=>"opcode=>36 parameter1=>2 %default%"
     END
     LAF make_feats_core STR_VAR feat=LIGHTNING_REFLEXES END

     MAKE_PATCH
         add_effect_inline=>"opcode=>37 parameter1=>2 %default%"
     END
     LAF make_feats_core STR_VAR feat=IRON_WILL END

     MAKE_PATCH
         add_effect_inline=>"opcode=>325 parameter1=>1 %default%"
     END
     LAF make_feats_core STR_VAR feat=LUCK_OF_HEROES END
     
     // bardsongs

    // LAF bard_song END

     // Smite Evil
	 
	 ACTION_IF FILE_EXISTS_IN_GAME "%PALADIN_SMITE_EVIL%.spl" BEGIN
	   LAF edit_spell STR_VAR spell=PALADIN_SMITE_EVIL editstring="say_description=>85" END
	 END ELSE BEGIN
       MAKE_PATCH
           say_name=>84
           say_description=>85
           patch_effect_inline=>"match=>opcode=174 resource=>EFF_P15"
           patch_effect_inline'=>"match=>opcode=324 resource=>filename"
           not_magical=>0 // bug in IWDEE
       END
       LAF install_spell STR_VAR spell=spcl122 idsname=PALADIN_SMITE_EVIL edits=patch_data location=resource END // base spell is verbatim copy of IWD version
       LAF install STR_VAR locbase=resource files="spcl122b.bam spcl122c.bam" END
	 END
	 
	 // grant Sorcerer powers
	 
	 ACTION_FOR_EACH spell IN IDENTIFY CONTINGENCY CHAIN_CONTINGENCY SPELL_SEQUENCER SPELL_TRIGGER MINOR_SEQUENCER BEGIN
		OUTER_SPRINT ids "WIZARD_%spell%"
		OUTER_SPRINT resref "%%ids%%"
		COPY_EXISTING "%resref%.spl" override
			READ_LONG 0x8 namestring
			READ_LONG 0x50 descstring
			READ_LONG 0x34 level
			GET_STRREF namestring name
			SET level_req_strref=220+level
			SPRINT level_req_string (AT level_req_strref)
		BUT_ONLY
		OUTER_SPRINT desc_new @220
		OUTER_INNER_PATCH_SAVE desc_new "%desc_new%" BEGIN
			REPLACE_TEXTUALLY "PLACEHOLDER" "%name%"
		END
		OUTER_SPRINT desc_new "%desc_new%%WNL%%WNL%%require_string%%level_req_string%"
		MAKE_PATCH
			add_effect_inline=>"opcode=>171 resource=>%resref% %default%"
			set_name=>"%name%"
			set_description=>"%desc_new%"
		END
		LAF make_feats_core STR_VAR feat="SORCERER_GRANT_%spell%" END
	 
	 
	 END
	 
     // Healing touch-  clone of Cure Disease
     // Restoring touch - clone of Restoration
     // Blessed touch - clone of (cleric) Remove Curse

     ACTION_FOR_EACH name IN HEALING_TOUCH BLESSED_TOUCH RESTORING_TOUCH BEGIN
           LAF add_spell_to_ids STR_VAR idsname="PALADIN_%name%" RET "PALADIN_%name%"=newname END

     END
     MAKE_PATCH
		spell_type=>innate
        patch_ability_inline=>"ability_icon_loc=>4 casting_time=>1"
        patch_effect_inline=>"power=>1"
     END
     OUTER_SPRINT $patch_data("say_name") 90
     OUTER_SPRINT $patch_data("say_description") 98
     LAF clone_spell STR_VAR spell="%CLERIC_CURE_DISEASE%=>%PALADIN_HEALING_TOUCH%" edits=patch_data END
     OUTER_SPRINT $patch_data("say_name") 94
     OUTER_SPRINT $patch_data("say_description") 99
	 OUTER_SPRINT $patch_data("delete_effect") "opcode=93" // Paladin's touch doesn't cause fatigue
     LAF clone_spell STR_VAR spell="%CLERIC_LESSER_RESTORATION%=>%PALADIN_RESTORING_TOUCH%" edits=patch_data END
     OUTER_SPRINT $patch_data("say_name") 100
     OUTER_SPRINT $patch_data("say_description") 103
     LAF clone_spell STR_VAR spell="%CLERIC_REMOVE_CURSE%=>%PALADIN_BLESSED_TOUCH%" edits=patch_data END

	 ACTION_FOR_EACH power IN BLESSED_TOUCH HEALING_TOUCH RESTORING_TOUCH BEGIN
		OUTER_SPRINT grant_ids "PALADIN_GRANT_%power%"
		OUTER_SPRINT power_ids "PALADIN_%power%"
		OUTER_SPRINT power_resref EVAL "%%power_ids%%"
		MAKE_PATCH
			add_effect_inline=>"opcode=>171 resource=>%power_resref% %default%"
		END
		LAF make_feats_core STR_VAR feat="%grant_ids%" END
	 END

     // toughness

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>18 parameter1=>4"
     END
     LAF make_feats_core STR_VAR feat=TOUGHNESS END
     
     // Empowered casting
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>191 parameter1=>1 parameter2=>0"
        add_effect_inline'=>"%default% opcode=>191 parameter1=>1 parameter2=>0"
     END
     LAF make_feats_core STR_VAR feat=EMPOWERED_CASTING END

     // Greater empowered casting
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>191 parameter1=>2 parameter2=>0"
        add_effect_inline'=>"%default% opcode=>191 parameter1=>2 parameter2=>0"
     END
     LAF make_feats_core STR_VAR feat=GREATER_EMPOWERED_CASTING END

     // Empowered healing
     
   LAF resolve_stat INT_VAR ids=1 STR_VAR id=DW_POWER_UPGRADE12 RET stat_ind_low=stat_ind END
   LAF resolve_stat INT_VAR ids=1 STR_VAR id=DW_POWER_UPGRADE11 RET stat_ind_high=stat_ind END
   MAKE_PATCH
      add_basic_ability=>null
      add_effect_inline'=>"opcode=>328 mode=>1 timing=>9 target=>1 power=>1 parameter2=>%stat_ind_high%"
   END
   LAF make_spell STR_VAR spell="dwftemh2" edits=patch_data END
   MAKE_PATCH
      add_effect_inline=>"opcode=>326 target=>1 timing=>1 parameter1=>%stat_ind_low% parameter2=>110 resource=>dwftemh2"
      add_effect_inline'=>"at_end=>1 opcode=>318 target=>1 timing=>1 parameter1=>%stat_ind_low% parameter2=>110 resource=>filename"
      add_effect_inline''=>"at_end=>1 opcode=>328 mode=>1 timing=>9 target=>1 power=>1 parameter2=>%stat_ind_low%"
   END
   LAF make_feats_core STR_VAR feat=EMPOWERED_HEALING_FEAT END

     // Improved turning

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>323 parameter1=>1"
     END
     LAF make_feats_core STR_VAR feat=IMPROVED_TURNING END

     // Hardened Warrior
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>86 parameter1=>5"
        add_effect_inline'=>"%default% opcode=>87 parameter1=>5"
        add_effect_inline''=>"%default% opcode=>88 parameter1=>5"
        add_effect_inline'''=>"%default% opcode=>89 parameter1=>5"
     END
     LAF make_feats_core STR_VAR feat=HARDENED_WARRIOR END

     // Resistance
     
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>27 parameter1=>5"
        add_effect_inline'=>"%default% opcode=>28 parameter1=>5"
        add_effect_inline''=>"%default% opcode=>29 parameter1=>5"
        add_effect_inline'''=>"%default% opcode=>30 parameter1=>5"
        add_effect_inline''''=>"%default% opcode=>31 parameter1=>5"
     END
     LAF make_feats_core STR_VAR feat=RESISTANCE END

     // skill boosts

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>90 parameter1=>10" // open locks
        add_effect_inline'=>"%default% opcode=>92 parameter1=>10" // pick pockets
     END
     LAF make_feats_core STR_VAR feat=NIMBLE_FINGERS END

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>59 parameter1=>10" //  move silently
        add_effect_inline'=>"%default% opcode=>275 parameter1=>10" // hide in shadows
     END
     LAF make_feats_core STR_VAR feat=STEALTHY END

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>91 parameter1=>10" // find traps
        add_effect_inline'=>"%default% opcode=>276 parameter1=>10" // detect illusions
     END
     LAF make_feats_core STR_VAR feat=PERCEPTIVE END

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>277 parameter1=>15" // set traps
     END
     LAF make_feats_core STR_VAR feat=TRAPMASTER END

     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>21 parameter1=>30" // lore
     END
     LAF make_feats_core STR_VAR feat=LOREMASTER END


     // extra Lx spells

     ACTION_FOR_EACH level IN 1 2 3 4 5 6 7 8 BEGIN
        OUTER_SET levelcode=2 ** (level - 1)
        MAKE_PATCH
          add_effect_inline=>"%default% opcode=>215 resource=>spcontin" // animation
          add_effect_inline'=>"%default% opcode=>42 parameter1=>1 parameter2=>%levelcode%"
        END
        LAF make_feats_core STR_VAR feat="EXTRA_LEVEL_%level%" END
     END

     // extra ability X

     OUTER_SET number=1
     ACTION_FOR_EACH spell IN
     SET_SNARE_TRAP BARBARIAN_RAGE BERSERKER_RAGE ASSASSIN_POISON PALADIN_SMITE_EVIL PALADIN_LAY_ON_HANDS KENSAI_KIA INQUIS_TRUE_SIGHT INQUIS_DISPEL_MAGIC
     BLADE_DEFENSIVE_SPIN BLADE_OFFENSIVE_SPIN SPDWD02 SET_SPECIAL_SNARE_TRAP ARCHER_CALL_SHOT TOTEMIC_DRUID_SUMMON_SPIRIT_ANIMAL
     TALOS_LIGHTNING_BOLT TALOS_STORMSHIELD LATHANDER_BOON LATHANDER_HOLD_UNDEAD HELM_TRUE_SIGHT HELM_SEEKING_SWORD OHTYR1 OHTYR2 OHTMPS1 OHTMPS2
     SUN_SOUL_SUN_SOULRAY SUN_SOUL_GREATER_SUN SUN_SOUL_SUN_SOULBEAM MONK_QUIVERING_PALM SUN_SOUL_FLAMING_FISTS
     DARK_MOON_BLINDNESS DARK_MOON_BLUR DARK_MOON_MIRROR_IMAGE DARK_MOON_VAMPIRIC_TOUCH DARK_MOON_FROZEN_FIST MONK_LAY_ON_HANDS MONK_STUNNING_BLOW
	 BLACKGUARD_ABSORB_HEALTH BLACKGUARD_AURA_OF_DESPAIR
     BEGIN
        LAF extra_ability_feats STR_VAR spell number END
        OUTER_SET number +=1

     END
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_feats_core // violates encapsulation, so sue me. 
    STR_VAR feat="" feat_resref=""
BEGIN
    ACTION_IF "%feat_resref%" STRING_EQUAL "" BEGIN
       OUTER_SPRINT spell $feat_files("%feat%")
    END ELSE BEGIN
       OUTER_SPRINT spell "%feat_resref%"
    END
    LAF edit_spell STR_VAR spell edits=patch_data END
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION grant_ability
   STR_VAR feat=""
           feat_resref=""
           spell=""
           resref=""
           number_to_add=""
BEGIN
   ACTION_IF "%resref%" STRING_EQUAL "" BEGIN
      ACTION_IF VARIABLE_IS_SET "%spell%" BEGIN
        OUTER_SPRINT resref "%%spell%%"
      END ELSE BEGIN
        OUTER_SET idscode=IDS_OF_SYMBOL (spell "%spell%")
        LAF idscode_to_spell STR_VAR idscode RET resref END
      END
   END
   MAKE_PATCH
      add_effect_inline=>"%default% opcode=>171 resource=>%resref% number_to_add=>%number_to_add%"
   END
   LAF make_feats_core STR_VAR feat feat_resref END
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION hated_enemies BEGIN

	LAF warning STR_VAR warning="Hated enemy code ought really to remove the baseline enemy from the character sheet" END

    OUTER_SPRINT extra_string @58
	OUTER_SPRINT nemesis_extra @3062
    OUTER_SPRINT intro @57
    OUTER_SPRINT intro_nemesis @3061

    COPY_EXISTING haterace.2da override
           COUNT_2DA_ROWS 4 rowcount
           FOR (i=0;i<rowcount;i+=1) BEGIN
              READ_2DA_ENTRY i 1 4 name
              READ_2DA_ENTRY i 2 4 ids
              READ_2DA_ENTRY i 3 4 desc
              INNER_ACTION BEGIN
                 LAF hated_enemies_core INT_VAR ids name desc  END
              END
           END
    BUT_ONLY

///////////////////////////////////////////////////////////////////////////////////////////////////////////
END

DEFINE_ACTION_FUNCTION hated_enemies_core
     INT_VAR ids=0 name=0 desc=0
BEGIN
     // make eff files

     MAKE_PATCH
        parameter1=>"%ids%"
        parameter3=>4
     END
     LAF clone_effect STR_VAR effect="demhit3=>dwhah%ids%" edits=patch_data END
     LAF clone_effect STR_VAR effect="demdam3=>dwhad%ids%" edits=patch_data END

     // make name

     OUTER_PATCH "" BEGIN
        GET_STRREF "%name%" name_base
        GET_STRREF "%desc%" descstring
     END
     OUTER_SPRINT abil_name "%intro% %name_base%"
     OUTER_SPRINT hla_name "%intro_nemesis% %name_base%"
     OUTER_SET name_tlk = RESOLVE_STR_REF ("%abil_name%")
     OUTER_SET name_tlk_hla = RESOLVE_STR_REF ("%hla_name%")
	 OUTER_SPRINT descstring_base "%descstring%%WNL%%WNL%%extra_string%"
	 OUTER_SPRINT descstring_nemesis "%descstring%%WNL%%WNL%%nemesis_extra%"

     // make initial spells

     OUTER_PATCH_SAVE ids_name "%name_base%" BEGIN
        REPLACE_TEXTUALLY " " "_"
     END
     ACTION_TO_UPPER ids_name

     LAF setup_feats_core STR_VAR feat_id="RACIAL_ENEMY_%ids_name%" spell="dw#ha%ids%" namestring="%abil_name%" descstring="%descstring_base%" effect=~-1~ icon="" type="apply" END
     LAF setup_feats_core STR_VAR feat_id="RACIAL_NEMESIS_%ids_name%" spell="dw#hn%ids%" namestring="%hla_name%" descstring="%descstring_nemesis%" effect=~-1~ icon="" type="apply" END

     // add effects

     MAKE_PATCH
        add_effect_inline=>"opcode=>177 timing=>9 target=>1 parameter2=>2 resource=>dwhah%ids%"
        add_effect_inline'=>"opcode=>177 timing=>9 target=>1 parameter2=>2 resource=>dwhad%ids%"
     END
     LAF edit_spell STR_VAR spell="dw#ha%ids%" edits=patch_data END

     // make HLA version

     MAKE_PATCH
        add_effect_inline=>"opcode=>177 timing=>9 target=>1 parameter2=>2 resource=>dwhad%ids%"
        add_effect_inline'=>"opcode=>219 timing=>9 target=>1 parameter1=>%ids% parameter2=>4"
     END
     LAF edit_spell STR_VAR spell="dw#hn%ids%" edits=patch_data END

END



///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bard_song BEGIN

     // install bardsongs (nb: the Tale of Curran Strongheart subs for the generic)
     
     OUTER_SPRINT feat_files_VANILLA_BARD FJBARD

     <<<<<<<< .../stratagems-inline/bardsong.2da
     file_old     file_new          name             desc     prereq projectile    feat
     3heroes      dw#bard1          122              123      *      158           BALLAD_OF_THREE_HEROES
     curran       dw#bard2          120              121      *      158           VANILLA_BARD
     tymora       dw#bard3          123              124      134    158           TYMORAS_MELODY
     siren        dw#bard4          125              126      135    159           SIRENS_YEARNING
     sith         dw#bard5          127              128      136    158           WAR_CHANT_OF_SITH
     >>>>>>>>

     LAF process_table STR_VAR table=bardsong.2da inline=yes function=bard_song_core END
     
     <<<<<<<< .../stratagems-inline/bardsong_old.2da
     song_id    songname   spellname name       description
     skald      SPCL542A   SPCL542   130        138
     jester     SPCL751A   SPCL751   131        137
     epic       rr#bdf04   DW#FTBDE  132        139
     epic_skald rr#bsk02   DW#FTSKE  200        199
     epic_jest  rr#bjs02   DW#FTJES  -1         201
     >>>>>>>>

     LAF process_table STR_VAR table=bardsong_old.2da inline=yes function=bard_selector END

     // edit the epic powers to remove old effect and grant new one
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>171 resource=>DW#FTBDE"
        add_effect_inline''=>"opcode=>172 %default% resource=>DW#BARD1"
     END
     LAF edit_spell STR_VAR spell=SPCL920 edits=patch_data END
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>171 resource=>DW#FTSKE"
        add_effect_inline''=>"opcode=>172 %default% resource=>SPCL542"
     END
     LAF edit_spell STR_VAR spell=RR#BSK01 edits=patch_data END
     MAKE_PATCH
        add_effect_inline=>"%default% opcode=>171 resource=>DW#FTBDE"
        add_effect_inline''=>"opcode=>172 %default% resource=>SPCL751"
     END
     LAF edit_spell STR_VAR spell=RR#BJS01 edits=patch_data END

     // a few missing resources
     
     LAF install STR_VAR locbase="resource" files="bard1.bam bard2.bam bard3.bam bard4.bam bard5.bam bard6.bam" END
     LAF install_spell STR_VAR locbase=resource spell="#cureent" editstring="patch_effect_inline=>~resource=>DW#BARD4~" END

     // setup new-system bardsong for baseline bard

END
///////////////////////////////////////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION bard_song_core
     STR_VAR file_old="" file_new="" name=0 desc=0 projectile=0 feat="" prereq=0
BEGIN
     // make description
     OUTER_SPRINT namestring (AT "%name%")
     OUTER_SPRINT descstring (AT "%desc%")
     ACTION_IF "%prereq%" STRING_COMPARE "*" BEGIN 
          OUTER_SPRINT descstring "%namestring%%WNL%%descstring%%WNL%%prereq%"
     END ELSE BEGIN
          OUTER_SPRINT descstring "%namestring%%WNL%%descstring%"
     END
     OUTER_SET desc_strref=RESOLVE_STR_REF ("%descstring%")
     OUTER_SET name_strref=RESOLVE_STR_REF ("%namestring%")

     // install actual song

     MAKE_PATCH
        name1_string=>"%name_strref%"
        patch_ability_inline=>"projectile=>%projectile%"
        patch_effect_inline=>"match=>opcode=142 parameter2=>40"
        patch_effect_inline'=>"match=>opcode=321 resource=>%file_new%"
        patch_effect_inline''=>"match=>opcode=324 resource=>%file_new%"
     END
     LAF install_spell STR_VAR spell="bardsong_%file_old%=>%file_new%" edits=patch_data location=resource END

     // sort out power-granters
     OUTER_SPRINT spellname $feat_files("%feat%")
     LAF bard_selector INT_VAR name_strref desc_strref STR_VAR spellname songname="%file_new%" END



END


///////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bard_selector
   INT_VAR name=0
           description=0
           name_strref=0
           desc_strref=0
   STR_VAR spellname=""
           songname=""
BEGIN
       MAKE_PATCH
          patch_effect_inline=>"match=>opcode=251 resource=>%songname%"
          patch_effect_global_inline=>"resource=>%spellname%"
       END
       ACTION_IF (name_strref=0 && name >=0) BEGIN
          OUTER_SET name_strref=RESOLVE_STR_REF ((AT "%name%"))
       END
       ACTION_IF (desc_strref=0 && description >=0) BEGIN
          OUTER_SET desc_strref=RESOLVE_STR_REF ((AT "%description%"))
       END
       ACTION_IF name_strref>0 BEGIN
           OUTER_SET $patch_data(name1_string) = name_strref
       END
       ACTION_IF desc_strref>0 BEGIN
           OUTER_SET $patch_data(description1_string) = desc_strref
       END
       LAF install_spell STR_VAR location=resource spell="#bard1=>%spellname%" edits=patch_data END

END



///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION extra_ability_feats
        INT_VAR number=1
        STR_VAR spell=""
BEGIN
        ACTION_TO_UPPER spell
        OUTER_SPRINT name_start @185
        OUTER_SPRINT desc_start @186
        ACTION_IF VARIABLE_IS_SET "%spell%" BEGIN
              OUTER_SPRINT resref "%%spell%%"
        END ELSE BEGIN
              OUTER_SET idscode=IDS_OF_SYMBOL (spell "%spell%")
              ACTION_IF idscode>=0 BEGIN
                 LAF idscode_to_spell STR_VAR idscode RET resref END
              END ELSE BEGIN
                 OUTER_SPRINT resref "%spell%"
              END
        END
        COPY_EXISTING ~%resref%.spl~ override
           GET_STRREF (LONG_AT 0x8) spellname
           READ_LONG 0x64 offset_base
           LPF SPL_read_ability_icon STR_VAR offset_base RET icon=value END
        BUT_ONLY
		OUTER_SPRINT take_multiple_string @207
        OUTER_SPRINT namestring "%name_start% %spellname%"
        OUTER_SPRINT descstring "%desc_start% %spellname%.%WNL%%WNL%%take_multiple_string%%WNL%%WNL%%require_string%%spellname%."
        OUTER_SPRINT feat_resref "dw#ftx%number%"
        LAF setup_feats_core STR_VAR feat_id="EXTRA_%spell%" spell="%feat_resref%" namestring descstring icon type="apply" END
        LAF grant_ability STR_VAR feat_resref resref number_to_add=1 END
        LAF log_this STR_VAR file=autofeat.2da input="EXTRA_%spell%   %feat_resref%   apply" END
END

