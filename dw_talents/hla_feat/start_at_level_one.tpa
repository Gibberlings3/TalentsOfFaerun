DEFINE_ACTION_FUNCTION start_at_level_one BEGIN

	// handle mage/bard spells - do this first, before we clear out the xp line
	
	LAF level_one_find_starting_xp RET xp END
	LAF level_one_set_spell_table INT_VAR xp STR_VAR class=mage table=MXSPLWIZ END
	LAF level_one_set_spell_table INT_VAR xp STR_VAR class=bard table=MXSPLBRD END

	//main part

	ACTION_CLEAR_ARRAY startare2das 
	OUTER_SPRINT $startare2das("STARTARE") ""
	OUTER_SPRINT $startare2das("STARTBP") ""
	ACTION_IF FILE_EXISTS_IN_GAME "campaign.2da" BEGIN
		LAF 2da_read STR_VAR file=campaign.2da RET_ARRAY campaign_rows=rows campaign_array=array END
		ACTION_PHP_EACH campaign_rows AS row=>discard BEGIN
			OUTER_SPRINT 2da $campaign_array("%row%" STARTARE)
			ACTION_TO_UPPER 2da
			OUTER_SPRINT $startare2das("%2da%") ""
		END
	END
	
	ACTION_PHP_EACH startare2das AS file=>discard BEGIN
		LAF start_at_level_one_core STR_VAR file END
	END

	// handle CHR files
	
	LAF start_at_level_one_pregens END
	

END

DEFINE_ACTION_FUNCTION start_at_level_one_core
	STR_VAR file=""
BEGIN
	COPY_EXISTING "%file%.2da" override
		LPF 2da_read RET_ARRAY start_array=array END
		SPRINT area $start_array(START_AREA VALUE)
		xpstart=$start_array(START_XP VALUE) - 1
		LPF extend_area_script INT_VAR inline=1 STR_VAR area top=xpstart END
		SET $start_array(START_XP VALUE)=1
		PATCH_IF VARIABLE_IS_SET $start_array(START_MP_XP VALUE) && VARIABLE_IS_SET $start_array(START_AREA_MP VALUE) BEGIN
			SPRINT area $start_array(START_AREA_MP VALUE)
			xpstart=$start_array(START_MP_XP VALUE) - 1
			LPF extend_area_script INT_VAR inline=1 STR_VAR area top=xpstart END
			SET $start_array(START_MP_XP VALUE)=1
		END
		LPF 2da_write STR_VAR array=start_array default="BADVAL" END
	BUT_ONLY
	IF_EXISTS

END

DEFINE_ACTION_FUNCTION level_one_find_starting_xp RET xp BEGIN


	ACTION_IF GAME_IS "IWDEE" BEGIN
		OUTER_SPRINT startare startare
		LAF check_ini STR_VAR ini=start_iwd section=default_start RET value END
		ACTION_IF value STR_EQ "how" BEGIN
			OUTER_SPRINT line START_MP_XP
		END ELSE BEGIN
			OUTER_SPRINT line START_XP
		END
	END ELSE
	ACTION_IF GAME_IS "EET" BEGIN
		OUTER_SPRINT line START_XP
		LAF check_ini STR_VAR ini=start_eet section=default_start RET value END
		ACTION_MATCH "%value%" WITH
		sod BEGIN
			OUTER_SPRINT startare sodstrta
		END
		soa BEGIN
			OUTER_SPRINT startare startare	
		END
		tob BEGIN
			OUTER_SPRINT startare 25starta
		END
		bp bp1 BEGIN
			OUTER_SPRINT startare startbp_
		END
		bp2 BEGIN
			OUTER_SPRINT startare startbp
		END
		bg bg1 BEGIN
			OUTER_SPRINT startare bgstarta
		END
		DEFAULT
			WARN "hla_feat function start_at_level_1: unrecognized value %value% for ini start_eet, assuming BG"
			OUTER_SPRINT startare bgstarta
		END
	END ELSE
	ACTION_IF GAME_IS "BG2EE" BEGIN
		LAF check_ini STR_VAR ini=start_bg2 section=default_start RET value END
		ACTION_MATCH "%value%" WITH
		bp2 BEGIN
			OUTER_SPRINT startare startbp
			OUTER_SPRINT line START_XP
		END
		tob BEGIN
			OUTER_SPRINT startare startare
			OUTER_SPRINT line START_MP_XP		
		END
		DEFAULT
			OUTER_SPRINT startare startare
			OUTER_SPRINT line START_XP		
		END
	END ELSE BEGIN // assume BGEE
		LAF check_ini STR_VAR ini=start_bg section=default_start RET value END
		ACTION_MATCH "%value%" WITH
		bp bp1 BEGIN
			OUTER_SPRINT startare startbp
			OUTER_SPRINT line START_XP
		END
		sod BEGIN
			ACTION_IF GAME_INCLUDES "SOD" BEGIN
				OUTER_SPRINT startare sodstrta
			END ELSE BEGIN
				WARN "ini value start_bg is set to sod but sod is not installed; reverting to bg"
				OUTER_SPRINT startare startare
			END
			OUTER_SPRINT line START_XP		
		END
		DEFAULT
			OUTER_SPRINT startare startare
			OUTER_SPRINT line START_XP				
		END
	END
	LAF 2da_read STR_VAR file="%startare%.2da" RET_ARRAY startare_array=array END
	OUTER_SET xp=$startare_array("%line%" VALUE)



END

DEFINE_ACTION_FUNCTION level_one_set_spell_table
	INT_VAR xp=0
	STR_VAR table=""
			class=""
BEGIN
	// restore backup if appropriate, else make one
	ACTION_IF FILE_EXISTS "%data_loc%/%table%_backup.2da" BEGIN
		COPY "%data_loc%/%table%_backup.2da" "override/%table%.2da"
	END ELSE BEGIN
		COPY_EXISTING  "%table%.2da" "%data_loc%/%table%_backup.2da"
	END
	// find level
	LAF level_at_xp INT_VAR xp STR_VAR class RET level END
	// make changes
	COPY_EXISTING "%table%.2da" override
		LPF 2da_read RET_ARRAY spell_array=array spell_rows=rows spell_cols=columns END
		proceed=0
		PHP_EACH spell_rows AS row=>discard BEGIN
			proceed=proceed|| (row=level)
		END
		PATCH_IF proceed BEGIN
			PHP_EACH spell_cols AS col=>discard BEGIN	
				SET $spell_array("1" "%col%")=$spell_array("%level%" "%col%")
			END
		END
		LPF 2da_sort STR_VAR rows=numerically array=spell_array RET_ARRAY spell_array=array END
		LPF 2da_write STR_VAR array=spell_array END
	BUT_ONLY
END

DEFINE_ACTION_FUNCTION start_at_level_one_pregens BEGIN

	// get data
	LAM data_thac0
	LAM data_saving_throws

	// parse kitlist and hp table
	LAF 2da_read STR_VAR file=kitlist.2da RET_ARRAY kitlist_rows=rows kitlist_array=array END
	LAF 2da_read STR_VAR file=hpclass.2da RET_ARRAY hpclass_array=array END

	ACTION_CLEAR_ARRAY thief_skills_offsets
	ACTION_DEFINE_ASSOCIATIVE_ARRAY thief_skills_offsets BEGIN
		hide_in_shadows=>0x45
		detect_illusion=>0x64
		set_traps=>0x65
		lore=>0x66
		open_locks=>0x67
		move_silently=>0x68
		find_traps=>0x69
		pick_pockets=>0x6a
	END

	COPY_EXISTING_REGEXP ".*\.chr" override
		// check if level 1; if so, skip
		PATCH_IF !(BYTE_AT (0x234 + 0x64)=1) BEGIN
			// remove the chr header temporarily
			READ_ASCII 0x0 header (0x64)
			DELETE_BYTES 0x0 0x64
			// set level
			classid=BYTE_AT 0x273
			LOOKUP_IDS_SYMBOL_OF_INT class class classid
			PATCH_MATCH "%classid%" WITH
			".*_.*_.*" BEGIN
				WRITE_BYTE 0x234 1
				WRITE_BYTE 0x235 1
				WRITE_BYTE 0x236 1
			END
			".*_.*" BEGIN
				WRITE_BYTE 0x234 1
				WRITE_BYTE 0x235 1
				WRITE_BYTE 0x236 0		
			END
			DEFAULT
				WRITE_BYTE 0x234 1
				WRITE_BYTE 0x235 0
				WRITE_BYTE 0x236 0		
			END
			// find kit
			LPF cre_read_kit RET kitids=value END
			PATCH_IF kitids=16384 || kitids=0 BEGIN
				SPRINT kit "%class%"
			END ELSE BEGIN
				PHP_EACH kitlist_rows AS row=>discard BEGIN
					PATCH_IF IS_AN_INT $kitlist_array("%row%" "KITIDS") && $kitlist_array("%row%" "KITIDS")=kitids BEGIN
						SPRINT kit $kitlist_array("%row%" "ROWNAME")
					END
				END
			END
			TO_UPPER kit
			// set hit points
			SPRINT hptable $hpclass_array("%kit%" "TABLE")
			INNER_ACTION BEGIN // not superefficient, but we're not editing many of these
				LAF 2daq_extract STR_VAR resref="%hptable%" row=1 RET_ARRAY hp_array=array END
			END
			hp=$hp_array("SIDES")*$hp_array("ROLLS") + $hp_array("MODIFIER")
			WRITE_SHORT 0x24 hp
			WRITE_SHORT 0x26 hp
			// set thief skills
			PHP_EACH thief_skills_offsets AS skill=>discard BEGIN
				SET $thief_skills("%skill%")=0
			END
			PATCH_MATCH "%class%" WITH
			RANGER BEGIN
				INNER_ACTION BEGIN
					LAF 2daq_extract STR_VAR resref=SKILLRNG row=1 column=MOVE_SILENTLY RET value END
				END
				SET $thief_skills("move_silently")=value				
			END
			SHAMAN BEGIN
				INNER_ACTION BEGIN
					LAF 2daq_extract STR_VAR resref=SKILLSHM row=1 column=DETECT_ILLUSION RET value END
				END
				SET $thief_skills("detect_illusion")=value		
			END
			THIEF WHEN "%kit%" STR_EQ "SHADOWDANCER" BEGIN
				SET $thief_skills("move_silently")=15
				SET $thief_skills("hide_in_shadows")=15
			END
			THIEF BEGIN
				SET $thief_skills("open_locks")=20
				SET $thief_skills("find_traps")=20
			
			END
			BARD BEGIN
				INNER_ACTION BEGIN
					LAF 2daq_extract STR_VAR resref=SKILLBRD row=1 column=PICK_POCKETS RET value END
				END
				SET $thief_skills("pick_pockets")=value
			END
			DEFAULT
			END
			PATCH_MATCH "%class%" WITH
			".*MAGE.*" ".*THIEF.*" BEGIN
				SET $thief_skills("lore")=3		
			END
			"BARD" BEGIN
				SET $thief_skills("lore")=10		
			END
			DEFAULT
				SET $thief_skills("lore")=1
			END
			// set saves and THAC0
			LPF cre_enforce_saves_notstruct END
			LPF cre_enforce_thac0_notstruct END
			
			// put the chr header back
			INSERT_BYTES 0x0 0x64
			WRITE_ASCII 0x0 "%header%"
		END
	
	BUT_ONLY
	
	LAF run STR_VAR file=prof_npc locbase=prof version=chr END
	


END

<<<<<<<<.../stratagems-inline/xpstart.baf
IF
	Global("DW_start_area_xp","GLOBAL",0)
	XPLT(Player1,1000)
THEN
	RESPONSE #100
		SetGlobal("DW_start_area_xp","GLOBAL",1)
		AddXPObject(Player1,%xpstart%)
		AddXPObject(Player2,%xpstart%)
		AddXPObject(Player3,%xpstart%)
		AddXPObject(Player4,%xpstart%)
		AddXPObject(Player5,%xpstart%)
		AddXPObject(Player6,%xpstart%)
		Continue()
END
>>>>>>>>
