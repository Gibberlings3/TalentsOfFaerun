DEFINE_ACTION_FUNCTION run
	INT_VAR live_code=0
			marker=1
	STR_VAR file="" // name of the file containing this component's main function (you cna omit the suffix if it's 'tpa')
			files="" // legacy synonym for 'file'
			function="" // name of the main function (default value: same as file)
			location="" // path to the function (relative to %MOD_FOLDER%) - if blank, we assume 'file' is the full path, including MOD_FOLDER
			tra="" // tra file to be loaded automatically before the main function is run (relative to %sfo_tra_loc%/%LANGUAGE% - leave blank if you don't need a tra file automatically used)
			version="" // optional STR_VAR to be sent to the main function
BEGIN
	ACTION_IF IS_AN_INT SFO_batch_mode && SFO_batch_mode BEGIN // if the 'batch mode' variable is set
		// do nothing! All we care about is recording the component as installed
	END ELSE BEGIN
		ACTION_IF "%files%" STR_CMP "" BEGIN
			OUTER_SPRINT file "%files%"
		END
		ACTION_IF "%function%" STR_EQ "" BEGIN
			OUTER_SPRINT function "%file%"
		END
		ACTION_IF INDEX ("\." "%file%")<0 BEGIN
			OUTER_SPRINT file "%file%.tpa"
		END
		ACTION_IF "%location%" STR_CMP "" BEGIN
			OUTER_SPRINT file "%MOD_FOLDER%/%location%/%file%"
		END
		ACTION_IF !VARIABLE_IS_SET "%sfo_tra_loc%" BEGIN
			OUTER_SPRINT sfo_tra_loc "%MOD_FOLDER%/lang"
		END
		WITH_TRA "%sfo_tra_loc%/english/%tra%.tra" "%sfo_tra_loc%/%LANGUAGE%/%tra%.tra" BEGIN
			INCLUDE "%file%"
			ACTION_IF !live_code BEGIN
				ACTION_IF "%version%" STR_EQ "" BEGIN
					LAF "%function%" END
				END ELSE BEGIN
					LAF "%function%" STR_VAR version END
				END
			END
		END	
	END
END

DEFINE_ACTION_MACRO batch_mode_initialize
BEGIN
	OUTER_SET SFO_batch_mode=1
	ACTION_IF "%WEIDU_OS%" STR_CMP "win32" BEGIN
		PRINT "WARNING: batch mode for non-Windows systems is highly experimental and largely untested. Use at own risk; report any problems at the mod forum"
	END
END

DEFINE_ACTION_FUNCTION batch_mode_conclude INT_VAR execute=1
BEGIN
	// get the list of components
	LAF batch_mode_collect_components RET_ARRAY component_array END
	// make the string
	OUTER_SPRINT string ""
	ACTION_PHP_EACH component_array AS int=>discard BEGIN
		OUTER_SPRINT string "%string%%int% "
	END
	// get the batch location
	ACTION_IF !VARIABLE_IS_SET batch_loc BEGIN
		OUTER_SPRINT batch_loc "weidu_external/batch"
	END

	// install the batch
	ACTION_IF "%WEIDU_OS%" STR_EQ win32 BEGIN
		ACTION_IF FILE_EXISTS "setup-%MOD_FOLDER%.exe" BEGIN
			COPY + ".../stratagems-inline/batch_template_win32.bat" "%batch_loc%/%MOD_FOLDER%.bat" EVALUATE_BUFFER
		END ELSE BEGIN
			COPY + ".../stratagems-inline/batch_template_win32_no_setup.bat" "%batch_loc%/%MOD_FOLDER%.bat" EVALUATE_BUFFER
		END
	END ELSE BEGIN
		COPY + ".../stratagems-inline/batch_template_linux.sh" "%batch_loc%/%MOD_FOLDER%.sh" EVALUATE_BUFFER	
	END
	
	// run, if appropriate
	
	ACTION_IF execute BEGIN
		ACTION_IF "%WEIDU_OS%" STR_EQ win32 BEGIN
			AT_EXIT "%batch_loc%/%MOD_FOLDER%.bat"
		END ELSE BEGIN
			AT_EXIT "sh %batch_loc%/%MOD_FOLDER%.sh"		
		END
	END ELSE BEGIN
		ACTION_IF FILE_EXISTS "setup-%MOD_FOLDER%.exe" BEGIN // windows, and setup-blah present
			AT_EXIT "setup-%MOD_FOLDER% --uninstall"	
		END ELSE BEGIN
			AT_EXIT "weidu %MOD_FOLDER%/%TP2_FILE_NAME% --uninstall" // I'm not sure this works on linux or mac
		END
	END

END

DEFINE_ACTION_FUNCTION batch_mode_collect_components 
RET_ARRAY component_array
BEGIN

	ACTION_CLEAR_ARRAY component_array
	COPY - "%MOD_FOLDER%/%TP2_FILE_NAME%" nowhere
		REPLACE_EVALUATE "DESIGNATED[ %TAB%]+\([0-9]+\)" BEGIN
			SET component="%MATCH1%"
			PATCH_IF MOD_IS_INSTALLED "%TP2_FILE_NAME%" component BEGIN
				SET $component_array("%MATCH1%")=0
			END			
		END
		""
END



<<<<<<<<.../stratagems-inline/batch_template_win32.bat
setup-%MOD_FOLDER% --uninstall 
setup-%MOD_FOLDER% --force-install-list %string%>>>>>>>>
<<<<<<<<.../stratagems-inline/batch_template_win32_no_setup.bat
weidu %MOD_FOLDER%/%TP2_FILE_NAME% --uninstall
weidu %MOD_FOLDER%/%TP2_FILE_NAME% --force-install-list %string%>>>>>>>>
<<<<<<<<.../stratagems-inline/batch_template_linux.sh
weinstall %MOD_FOLDER%/%TP2_FILE_NAME% --uninstall
weinstall %MOD_FOLDER%/%TP2_FILE_NAME% --force-install-list %string%>>>>>>>>



<<<<<<<<.../stratagems-inline/247_check
ACTION_IF 0 BEGIN
	OUTER_SET x=RESOURCE_CONTAINS "blah" "blah"
END
>>>>>>>>

//live code!
ACTION_TRY
	INCLUDE ".../stratagems-inline/247_check"
WITH DEFAULT
	FAIL "ERROR: lib_batch requires WEIDU v247 or higher"
END