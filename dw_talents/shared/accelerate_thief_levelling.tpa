DEFINE_ACTION_FUNCTION accelerate_thief_levelling BEGIN
	LAF check_label STR_VAR label=dw#thief_ui_tweak RET value END
	ACTION_IF !value BEGIN
		LAF make_label STR_VAR label=dw#thief_ui_tweak END
		ACTION_FOR_EACH input IN true false BEGIN
			OUTER_SPRINT string "createCharScreen:OnThiefSkillPlusMinusButtonClick(chargen.thief_skill\[currentChargenThiefSkill\].id, %input%)"
			OUTER_SPRINT string_new_base "createCharScreen:OnThiefSkillPlusMinusButtonClick(chargen.thief_skill[currentChargenThiefSkill].id, %input%)"
			OUTER_SPRINT string_new "%string_new_base%%WNL%"
			OUTER_FOR (i=0;i<4;i+=1) BEGIN
				OUTER_SPRINT string_new "%string_new%%string_new_base%%WNL%"
			END
			COPY_EXISTING "ui.menu" override
				REPLACE_TEXTUALLY "%string%" "%string_new%"
			BUT_ONLY
		END

		// force multiples of five
		COPY_EXISTING "thiefskl.2da" override
			LPF 2da_read RET_ARRAY thiefskl_array=array thiefskl_rows=rows END 
			change=0
			PATCH_FOR_EACH col IN START_POINTS LEVEL_POINTS BEGIN
				PHP_EACH thiefskl_rows AS row=>discard BEGIN
					val=$thiefskl_array("%row%" "%col%")
					rem=val MODULO 5
					PATCH_IF rem>0 BEGIN
						change=1
						SET $thiefskl_array("%row%" "%col%")= ((val / 5) * 5) + ((rem>=3)? 5 : 0)
					END
				END
			END
			PATCH_IF change BEGIN
				LPF 2da_write STR_VAR array=thiefskl_array default="0" END
			END
		BUT_ONLY

	END
  

END
